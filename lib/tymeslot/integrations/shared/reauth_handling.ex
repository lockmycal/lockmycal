defmodule Tymeslot.Integrations.Shared.ReauthHandling do
  @moduledoc """
  Shared handler for the paths that end in "the user has to reconnect".

  The original caller was the "credentials no longer decrypt" path: when a
  query module returns `{:error, :requires_reencryption, integration}`, callers
  delegate here to log a warning, flag the integration for reauthentication,
  and return a normalised result. Permanent OAuth failures (an expired or
  revoked grant, credentials the provider now rejects) share the same
  side-effects but not the same diagnosis, so the reason is carried explicitly
  as a `t:cause/0`.

  Two distinct return shapes are available depending on context:

  - `flag/2` — returns `:ok | {:error, changeset}`. Suitable for non-Oban
    callers (fetch helpers, token refreshers) that just want the side-effect
    and a simple success/failure signal.

  - Oban workers that need `{:discard, _} | {:error, _}` build that shape
    themselves on top of `flag/2`, typically via the
    `CalendarManagement.handle_reauth_required/2` or
    `Video.handle_reauth_required/2` wrappers.
  """

  use Gettext, backend: TymeslotWeb.Gettext

  alias Tymeslot.Infrastructure.BreakerOutcome
  alias Tymeslot.Infrastructure.ErrorTracking
  alias Tymeslot.Infrastructure.Logging.LogFormat

  require Logger

  @typedoc """
  Why the integration needs reconnecting.

  * `:credentials_undecryptable` — the stored ciphertext no longer decrypts
    under the current encryption key.
  * `:expired_grant` — the OAuth grant has expired or the user revoked it.
  * `:rejected_credentials` — the provider rejected the credentials for some
    other permanent reason (`invalid_client`, `access_denied`, a bare 401).
  * `:rejected_subscription_url` — a published calendar feed rejected the
    stored subscription link (401/403), which means it was revoked or
    rotated rather than that any credentials expired.
  """
  @type cause ::
          :credentials_undecryptable
          | :expired_grant
          | :rejected_credentials
          | :rejected_subscription_url

  @default_cause :credentials_undecryptable

  @discard_reason "Credentials require reauthentication"

  # One row per cause: the operator-facing log line and the message persisted
  # to the integration's `sync_error`, which the account owner reads. Keeping
  # them together stops the two from drifting, and stops a decryption
  # diagnosis being reused for an OAuth failure it does not describe.
  #
  # `message` is persisted as its English msgid, never translated here: the
  # flagging process's locale belongs to whoever happened to trigger the flag,
  # not to the owner who reads it. The dashboard translates it into the
  # viewer's locale when it renders (`ConnectionRow.reconnect_reason/1`).
  @causes %{
    credentials_undecryptable: %{
      log: "Integration credentials cannot be decrypted — flagging for reauth",
      message:
        dgettext_noop(
          "dashboard_integrations",
          "Stored credentials could not be decrypted with the current encryption key. Please reconnect the integration."
        )
    },
    expired_grant: %{
      log: "Integration authorisation has expired or been revoked — flagging for reauth",
      message:
        dgettext_noop(
          "dashboard_integrations",
          "Access to the connected account has expired or been revoked. Please reconnect the integration."
        )
    },
    rejected_credentials: %{
      log: "Integration credentials were rejected by the provider — flagging for reauth",
      message:
        dgettext_noop(
          "dashboard_integrations",
          "The provider rejected the stored credentials for this integration. Please reconnect the integration."
        )
    },
    rejected_subscription_url: %{
      log: "Calendar subscription feed rejected the stored link — flagging for reauth",
      message:
        dgettext_noop(
          "dashboard_integrations",
          "The calendar feed rejected the stored link. It was probably revoked or reset — subscribe again with a fresh URL."
        )
    }
  }

  @doc """
  The error message recorded when flagging an integration for reauth for the
  given `t:cause/0`: the untranslated msgid, exactly as persisted to
  `sync_error`.

  Exposed so tests can pin the persisted message without restating the copy.
  """
  @spec reauth_error_message(cause()) :: String.t()
  def reauth_error_message(cause), do: fetch_cause(cause).message

  @doc """
  The reason an Oban job is discarded with once its integration has been
  flagged for reauthentication. Only the owner can fix that, so workers
  declare it an expected outcome (`Tymeslot.Infrastructure.ExpectedJobOutcome`).
  """
  @spec discard_reason() :: String.t()
  def discard_reason, do: @discard_reason

  @doc """
  Which `t:cause/0` a permanent credential failure describes.

  `invalid_grant` and `:token_expired` mean the grant itself is gone: expired,
  or revoked by the user in their provider account. Anything else the provider
  refused counts as rejected credentials. The two get different messages
  because they send the owner to different places.

  A binary reason is matched on whole-word tokens, so the marker has to reach
  this function intact: a caller that replaces the provider's error text with
  its own wording loses the distinction.
  """
  @spec rejection_cause(term()) :: :expired_grant | :rejected_credentials
  def rejection_cause(:token_expired), do: :expired_grant

  def rejection_cause(reason) when is_binary(reason) do
    if "invalid_grant" in BreakerOutcome.error_tokens(reason),
      do: :expired_grant,
      else: :rejected_credentials
  end

  def rejection_cause({:exception, message}) when is_binary(message),
    do: rejection_cause(message)

  def rejection_cause(_reason), do: :rejected_credentials

  @doc """
  Flags an integration for reauthentication.

  Options (keyword list):

  - `:mark_needs_reauth` — a `(integration, message) -> {:ok, _} | {:error, changeset}`
    function that persists the flag. **Required.**
  - `:cause` — a `t:cause/0` selecting the diagnosis logged and persisted.
    Defaults to `:credentials_undecryptable`.
  - `:provider_label` — a function extracting the provider string from the
    integration, e.g. `& &1.provider`. Defaults to `& &1.provider`.
  - `:log_prefix` — a short label used in log messages, e.g. `"Calendar"` or
    `"Video"`. Defaults to `"Integration"`.

  Logs a warning, calls `mark_needs_reauth`, then:

  - Returns `:ok` on `{:ok, _}`.
  - Logs an error and returns `{:error, changeset}` on `{:error, changeset}`.
  """
  @spec flag(struct(), keyword()) :: :ok | {:error, Ecto.Changeset.t()}
  def flag(integration, opts) do
    mark_needs_reauth = Keyword.fetch!(opts, :mark_needs_reauth)
    provider_label_fun = Keyword.get(opts, :provider_label, & &1.provider)
    log_prefix = Keyword.get(opts, :log_prefix, "Integration")
    cause_key = cause_key(Keyword.get(opts, :cause, @default_cause))
    cause = fetch_cause(cause_key)

    provider = provider_label_fun.(integration)

    if cause_key == :credentials_undecryptable,
      do: report_undecryptable(integration, provider, log_prefix)

    Logger.warning(
      cause.log,
      integration_type: log_prefix,
      provider: provider,
      integration_id: integration.id,
      user_id: integration.user_id
    )

    case mark_needs_reauth.(integration, cause.message) do
      {:ok, _integration} ->
        :ok

      {:error, changeset} ->
        Logger.error(
          "Failed to persist needs_reauth flag",
          provider: provider,
          integration_id: integration.id,
          errors: LogFormat.reason(changeset.errors)
        )

        {:error, changeset}
    end
  end

  defp cause_key(cause) when is_map_key(@causes, cause), do: cause
  defp cause_key(_unknown), do: @default_cause

  defp fetch_cause(cause), do: Map.fetch!(@causes, cause_key(cause))

  # Credentials that no longer decrypt usually mean the encryption key was
  # lost or rotated: the operator's to fix, and never one integration's
  # alone. The job that meets them is discarded as an expected end, since
  # only a reconnect recovers that integration, so the failure is recorded
  # here instead. One call site and one reason, so however many
  # integrations are affected it is one error, and one new-error alert.
  defp report_undecryptable(integration, provider, log_prefix) do
    ErrorTracking.report_error(:credentials_undecryptable, nil, %{
      integration_type: log_prefix,
      provider: provider,
      integration_id: integration.id,
      user_id: integration.user_id
    })
  end
end
