defmodule Tymeslot.Integrations.Google.GoogleOAuthHelper do
  @moduledoc """
  Shared Google OAuth helper for all Google integrations.

  This module provides OAuth functionality for Google services including
  Calendar, Google Meet, and other Google APIs. It handles token exchange,
  state management, and provides flexible scope configuration.
  """

  alias Tymeslot.Clock
  alias Tymeslot.Infrastructure.Config
  alias Tymeslot.Integrations.Common.OAuth.{ErrorParser, IdToken, State, TokenExchange}
  alias Tymeslot.Integrations.Google.Endpoints
  alias Tymeslot.Integrations.Shared.OAuth.ProviderHelpers
  alias Tymeslot.Integrations.Shared.OAuth.TokenFlow

  require Logger

  @default_scopes %{
    openid: "openid",
    email: "email",
    calendar_events: "https://www.googleapis.com/auth/calendar.events",
    calendarlist_readonly: "https://www.googleapis.com/auth/calendar.calendarlist.readonly",
    meet: "https://www.googleapis.com/auth/meetings.space.created"
  }

  @doc """
  Generates the OAuth authorization URL for Google services.

  ## Parameters
    - user_id: The user ID for state management
    - redirect_uri: The callback URI after authorization
    - scopes: List of scope atoms or custom scope strings
    - options: Additional OAuth options (access_type, prompt, etc.)

  ## Examples
      authorization_url(123, "https://example.com/callback", [:calendar_events, :calendarlist_readonly])
      authorization_url(123, "https://example.com/callback", [:calendar_events, :calendarlist_readonly, :meet])
      authorization_url(123, "https://example.com/callback", ["custom.scope"])
  """
  @spec authorization_url(integer(), String.t(), list(atom() | String.t()), keyword()) ::
          String.t()
  def authorization_url(user_id, redirect_uri, scopes, options \\ []) do
    integration_id = Keyword.get(options, :integration_id)
    login_hint = Keyword.get(options, :login_hint)
    return_to = Keyword.get(options, :return_to)
    state = generate_state(user_id, integration_id, return_to: return_to)

    # Always include openid and email for account identification
    all_scopes = Enum.uniq([:openid, :email | scopes])
    scope_string = build_scope_string(all_scopes)

    # Re-auth uses select_account only; new connections use consent + select_account
    default_prompt = if integration_id, do: "select_account", else: "consent select_account"

    base_params = %{
      client_id: google_client_id(),
      redirect_uri: redirect_uri,
      response_type: "code",
      scope: scope_string,
      state: state,
      access_type: Keyword.get(options, :access_type, "offline"),
      prompt: Keyword.get(options, :prompt, default_prompt)
    }

    # Add any additional options
    params =
      options
      |> Keyword.drop([:access_type, :prompt, :integration_id, :login_hint])
      |> Enum.into(base_params)

    ProviderHelpers.build_authorization_url(
      Endpoints.authorize_url(),
      params,
      login_hint
    )
  end

  @doc """
  Exchanges authorization code for access and refresh tokens.

  ## Parameters
    - code: Authorization code from Google
    - redirect_uri: The same redirect URI used in authorization
    - state: State parameter for validation

  Returns {:ok, tokens} or {:error, reason}
  """
  @spec exchange_code_for_tokens(String.t(), String.t(), String.t() | nil) ::
          {:ok, map()} | {:error, String.t()}
  def exchange_code_for_tokens(code, redirect_uri, state \\ nil) do
    body = %{
      code: code,
      client_id: google_client_id(),
      client_secret: google_client_secret(),
      redirect_uri: redirect_uri,
      grant_type: "authorization_code"
    }

    case TokenFlow.exchange_code(Endpoints.token_url(), body, log_context: [provider: :google]) do
      {:ok, response} ->
        tokens = build_token_map(response)

        case validate_state(state) do
          {:ok, %{user_id: user_id} = state_data} ->
            {:ok,
             tokens
             |> Map.put(:user_id, user_id)
             |> Map.put(:integration_id, state_data.integration_id)}

          {:error, _reason} when is_nil(state) ->
            {:ok, tokens}

          {:error, reason} ->
            {:error, reason}
        end

      # No second log line here: `TokenFlow` already logs the status and the
      # redacted body, and names the provider too.
      {:error, {:http_error, status, body}} ->
        {:error, ErrorParser.build_message("OAuth token exchange failed", status, body)}

      {:error, {:network_error, reason}} ->
        {:error, "Network error during token exchange: #{inspect(reason)}"}
    end
  end

  @doc """
  Refreshes an access token using a refresh token.

  This is the Google Meet **video** refresh as well as the calendar one, and
  both log `provider: :google`, so a caller that can name the integration
  should: pass `log_context: [integration_id: id, user_id: user_id]` and the
  two stop being indistinguishable on the line.

  ## Parameters
    - refresh_token: The refresh token
    - current_scope: Current token scope (optional)
    - opts: `:log_context`, forwarded to `TokenExchange.refresh_access_token/3`

  Returns {:ok, tokens} or {:error, reason}
  """
  @spec refresh_access_token(String.t(), String.t() | nil, keyword()) ::
          {:ok, map()} | {:error, String.t()}
  def refresh_access_token(refresh_token, current_scope \\ nil, opts \\ []) do
    body = %{
      refresh_token: refresh_token,
      client_id: google_client_id(),
      client_secret: google_client_secret(),
      grant_type: "refresh_token"
    }

    # Add scope if provided to maintain same scope
    body = if current_scope, do: Map.put(body, :scope, current_scope), else: body

    # No second log line here: `TokenExchange` already logs the status and the
    # redacted body, and now names the provider too.
    case TokenExchange.refresh_access_token(Endpoints.token_url(), body,
           fallback_refresh_token: refresh_token,
           fallback_scope: current_scope,
           log_context: Keyword.merge(Keyword.get(opts, :log_context, []), provider: :google)
         ) do
      {:ok, tokens} ->
        {:ok, tokens}

      {:error, {:http_error, status, body}} ->
        {:error, ErrorParser.build_message("Token refresh failed", status, body)}

      {:error, {:network_error, reason}} ->
        {:error, "Network error during token refresh: #{inspect(reason)}"}
    end
  end

  @doc """
  Revokes a token at Google, ending the whole grant it belongs to.

  Google revokes per grant, not per token: revoking the refresh token also
  invalidates every access token issued under it, for every Tymeslot
  integration sharing that Google account's consent (Calendar and Meet use one
  OAuth client). A token Google no longer recognises — already revoked, or
  expired — answers `400 invalid_token`; that is the desired end state, so it
  counts as success.
  """
  @spec revoke_token(String.t()) :: :ok | {:error, term()}
  def revoke_token(token) when is_binary(token) do
    headers = [{"Content-Type", "application/x-www-form-urlencoded"}]

    case Config.http_client_module().post(
           Endpoints.revoke_url(),
           URI.encode_query(%{token: token}),
           headers,
           []
         ) do
      {:ok, %{status: 200}} ->
        :ok

      {:ok, %{status: 400, body: body}} ->
        if invalid_token_error?(body), do: :ok, else: {:error, {:http_error, 400}}

      {:ok, %{status: status}} ->
        {:error, {:http_error, status}}

      {:error, reason} ->
        {:error, {:network_error, reason}}
    end
  end

  defp invalid_token_error?(%{"error" => "invalid_token"}), do: true

  defp invalid_token_error?(body) when is_binary(body) do
    case Jason.decode(body) do
      {:ok, decoded} -> invalid_token_error?(decoded)
      {:error, _reason} -> false
    end
  end

  defp invalid_token_error?(_body), do: false

  @doc """
  Generates a secure state parameter for OAuth flow.
  """
  @spec generate_state(integer(), pos_integer() | nil, keyword()) :: String.t()
  def generate_state(user_id, integration_id \\ nil, opts \\ []) do
    State.generate(user_id, state_secret(), integration_id, opts)
  end

  @doc """
  Validates and extracts state data from state parameter.
  """
  @spec validate_state(String.t() | any()) ::
          {:ok, State.validated()} | {:error, String.t()}
  def validate_state(state) when is_binary(state) do
    State.validate(state, state_secret())
  end

  def validate_state(_invalid), do: {:error, "Invalid state parameter"}

  @doc """
  Returns the secret used to sign and verify Google OAuth `state` parameters.
  """
  @spec state_secret() :: String.t()
  def state_secret do
    Application.get_env(:tymeslot, :google_oauth)[:state_secret] ||
      System.get_env("GOOGLE_STATE_SECRET") ||
      raise "Google State Secret not configured"
  end

  # Private functions

  defp build_token_map(response) do
    expires_at = DateTime.add(Clock.utc_now(), response["expires_in"], :second)

    {provider_account_id, provider_account_email} =
      case IdToken.decode(response["id_token"]) do
        {:ok, claims} ->
          {claims.sub, claims.email}

        {:error, reason} ->
          if response["id_token"] do
            Logger.warning(
              "Failed to decode Google id_token — account dedup falling back to legacy match",
              reason: inspect(reason)
            )
          end

          {nil, nil}
      end

    %{
      access_token: response["access_token"],
      refresh_token: response["refresh_token"],
      expires_at: expires_at,
      scope: response["scope"],
      provider_account_id: provider_account_id,
      provider_account_email: provider_account_email
    }
  end

  defp build_scope_string(scopes) when is_list(scopes) do
    scopes
    |> build_scope_list()
    |> Enum.join(" ")
  end

  defp build_scope_list(scopes) when is_list(scopes) do
    Enum.map(scopes, fn
      scope when is_atom(scope) -> Map.get(@default_scopes, scope, to_string(scope))
      scope when is_binary(scope) -> scope
    end)
  end

  defp google_client_id do
    Application.get_env(:tymeslot, :google_oauth)[:client_id] ||
      System.get_env("GOOGLE_CLIENT_ID") ||
      raise "Google Client ID not configured"
  end

  defp google_client_secret do
    Application.get_env(:tymeslot, :google_oauth)[:client_secret] ||
      System.get_env("GOOGLE_CLIENT_SECRET") ||
      raise "Google Client Secret not configured"
  end
end
