defmodule Tymeslot.Emails.Templates.EmailVerification do
  @moduledoc """
  Email template for user email verification.
  """
  use Gettext, backend: TymeslotWeb.Gettext

  alias Tymeslot.Emails.Shared.{Buttons, Greeting, TemplateHelper, Text}
  alias Tymeslot.Infrastructure.Config

  # A first-contact email welcoming a new user in.
  @intent :confirmed

  @spec render(Tymeslot.Emails.EmailService.user_map(), String.t()) :: String.t()
  def render(user, verification_url) do
    mjml_content = """
    #{Text.centered_html(Greeting.html(user), padding: "8px 0 4px 0", font_size: "16px")}

    #{Text.centered_text(dgettext("emails_account", "We're glad you're here. One quick step and your calendar will be ready to go - please confirm your email below."), padding: "0 0 20px 0")}

    #{Buttons.action_button(@intent, dgettext("emails_account", "Confirm Email & Get Started"), verification_url, full_width: true, size: :large)}

    #{Text.system_footer_note(dgettext("emails_account", "For your security, this link expires in 24 hours. If you didn't sign up for %{app_name}, you can ignore this email.", app_name: Config.app_name()))}

    #{Text.divider(margin: "28px 0 16px 0")}

    #{Text.troubleshooting_link(verification_url)}
    """

    TemplateHelper.compile_system_template(
      mjml_content,
      dgettext("emails_account", "Account Verification"),
      dgettext("emails_account", "Welcome to %{app_name} - please verify your email.",
        app_name: Config.app_name()
      ),
      intent: @intent,
      eyebrow: dgettext("emails_account", "Welcome"),
      stage_title:
        dgettext("emails_account", "Welcome to %{app_name}", app_name: Config.app_name()),
      stage_subtitle: dgettext("emails_account", "Let's get you scheduling in under a minute.")
    )
  end

  @spec render_text(Tymeslot.Emails.EmailService.user_map(), String.t()) :: String.t()
  def render_text(user, verification_url) do
    """
    #{dgettext("emails_account", "Welcome to %{app_name}!", app_name: Config.app_name())}

    #{Greeting.text(user)}

    #{dgettext("emails_account", "We're excited to have you on board! To start scheduling meetings and simplify your calendar, please verify your email address.")}

    #{dgettext("emails_account", "Confirm Email & Get Started:")}
    #{verification_url}

    #{dgettext("emails_account", "For your security, this link expires in 24 hours. If you didn't sign up for %{app_name}, no further action is needed.", app_name: Config.app_name())}
    """
  end
end
