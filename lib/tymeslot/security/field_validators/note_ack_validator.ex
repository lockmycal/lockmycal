defmodule Tymeslot.Security.FieldValidators.NoteAckValidator do
  @moduledoc """
  Validates the acknowledgement payload for a `note` custom field. The
  shape `%{"confirmed" => true, "confirmed_at" => <iso8601>}` is the
  contract between the booking engine and the snapshot.
  """

  use Gettext, backend: TymeslotWeb.Gettext

  @spec validate(any(), map(), keyword()) :: :ok | {:error, String.t()}
  def validate(value, definition, opts \\ [])

  def validate(%{"confirmed" => true, "confirmed_at" => iso}, _definition, _opts)
      when is_binary(iso) do
    case DateTime.from_iso8601(iso) do
      {:ok, _dt, 0} -> :ok
      {:ok, _dt, _offset} -> {:error, dgettext("errors", "Confirmation timestamp must be UTC")}
      _err -> {:error, dgettext("errors", "Confirmation timestamp is invalid")}
    end
  end

  # A blank acknowledgement is acceptable when the host marked the note
  # optional — the editor exposes a Required toggle for notes, so an
  # optional note must not block bookers. Only a required note rejects nil.
  def validate(nil, _definition, opts), do: blank(opts)
  def validate("", _definition, opts), do: blank(opts)

  def validate(_value, _definition, _opts),
    do: {:error, dgettext("errors", "Please acknowledge to continue")}

  defp blank(opts) do
    if Keyword.get(opts, :required, true),
      do: {:error, dgettext("errors", "Please acknowledge to continue")},
      else: :ok
  end
end
