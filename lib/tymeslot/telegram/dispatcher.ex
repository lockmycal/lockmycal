defmodule Tymeslot.Telegram.Dispatcher do
  @moduledoc """
  Dispatches Telegram notifications when booking events occur.
  Mirrors the webhook dispatcher pattern.
  """

  require Logger

  alias Tymeslot.Meetings.MeetingSchema
  alias Tymeslot.Notifications.EventTypes
  alias Tymeslot.Telegram

  @spec dispatch(atom() | String.t(), MeetingSchema.t()) :: :ok | {:error, term()}
  def dispatch(event_atom, %MeetingSchema{} = meeting) when is_atom(event_atom) do
    event_type = atom_to_event_type(event_atom)
    dispatch(event_type, meeting)
  end

  def dispatch(event_type, %MeetingSchema{} = meeting) when is_binary(event_type) do
    case meeting.organizer_user_id do
      nil ->
        Logger.warning("Cannot dispatch Telegram: meeting has no organizer_user_id",
          meeting_id: meeting.id
        )

        {:error, :no_organizer}

      user_id ->
        Logger.debug("Dispatching Telegram notifications",
          user_id: user_id,
          event_type: event_type,
          meeting_id: meeting.id
        )

        Telegram.trigger_integrations_for_event(user_id, event_type, meeting)
        :ok
    end
  end

  @spec atom_to_event_type(atom()) :: String.t()
  defp atom_to_event_type(event_atom), do: EventTypes.to_event_type(event_atom)
end
