defmodule Tymeslot.Auth.OAuth.Providers do
  @moduledoc """
  The social sign-in providers, as data. Everything that differs between
  GitHub, Google, Microsoft and the generic OAuth/OIDC provider (SSO) lives in
  `@providers`; the flow, the HTTP client and the account lookup read it
  rather than branching per provider.

  Per provider:

    * `:slug` - the URL segment (`/auth/github`) and the stored `provider`
    * `:name` - the name shown to users; for the generic provider, the
      fallback when its configuration carries no `:name`
    * `:setting` - the `AppSettings` key that switches it on, read at request
      time because an admin can toggle it without a restart
    * `:uid_field` - the user column holding the provider's stable user ID
    * `:uid_claims` - the userinfo keys that carry that ID, in order of
      preference
    * `:email` - where a verified email comes from: `{:claim, key, absent}`
      when the userinfo response carries `key: true` beside the email, or
      `{:emails_endpoint, url}` for GitHub's list of addresses. `absent` says
      how the claim is read: `:unverified` (only `true` vouches), or
      `:operator_policy` for the generic provider, whose identity provider is
      the operator's own. `OAUTH_EMAIL_VERIFIED_CLAIM` sets that policy:
      `trust_absent` (the default: a missing claim vouches, `false` does not;
      Authentik, Entra ID and some Keycloak setups omit the claim), `require`
      (only `true` vouches) or `ignore` (every email the IdP returns vouches,
      for IdPs that send `false` for addresses they manage themselves).
      `:never_vouched` is Microsoft's: a work account's address is whatever
      its tenant admin typed (the "nOAuth" takeover), so whatever the
      userinfo says, the user always proves the address by email.
    * `:auth_scheme` - the `Authorization` scheme for userinfo requests
    * `:authorize_params` - extra authorise-URL parameters
    * `:lookup` - the `UserQueries` function (and leading arguments) that
      finds the account by its provider ID; named rather than captured, so
      the table stays free of compile-time dependencies

  Endpoints, scope and client credentials come from `config/1`, since the
  generic provider's are configured at runtime.

  Microsoft sign-in reuses the Outlook/Teams app registration
  (`Tymeslot.Integrations.Shared.MicrosoftConfig`) through the `common`
  endpoint, so both personal and work/school accounts can sign in.

  Every provider receives PKCE parameters: GitHub, Google, Microsoft and OIDC servers
  support S256, and RFC 6749 requires a server to ignore request parameters
  it does not recognise, so no per-provider switch is needed.
  """

  alias Tymeslot.AppSettings
  alias Tymeslot.Auth.UserQueries
  alias Tymeslot.Integrations.Google.Endpoints, as: GoogleEndpoints
  alias Tymeslot.Integrations.Shared.MicrosoftConfig

  @type provider :: :github | :google | :microsoft | :oauth

  @type config :: %{
          client_id: String.t() | nil,
          client_secret: String.t() | nil,
          authorize_url: String.t(),
          token_url: String.t(),
          userinfo_url: String.t(),
          scope: String.t()
        }

  @providers %{
    github: %{
      slug: "github",
      name: "GitHub",
      setting: :github_auth_enabled,
      uid_field: :github_user_id,
      uid_claims: ["id"],
      email: {:emails_endpoint, "https://api.github.com/user/emails"},
      auth_scheme: "token",
      authorize_params: %{},
      lookup: {:get_user_by_github_id, []}
    },
    google: %{
      slug: "google",
      name: "Google",
      setting: :google_auth_enabled,
      uid_field: :google_user_id,
      uid_claims: ["id"],
      email: {:claim, "verified_email", :unverified},
      auth_scheme: "Bearer",
      authorize_params: %{prompt: "select_account"},
      lookup: {:get_user_by_google_id, []}
    },
    microsoft: %{
      slug: "microsoft",
      name: "Microsoft",
      setting: :microsoft_auth_enabled,
      uid_field: :microsoft_user_id,
      uid_claims: ["sub"],
      email: :never_vouched,
      auth_scheme: "Bearer",
      authorize_params: %{prompt: "select_account"},
      lookup: {:get_user_by_microsoft_id, []}
    },
    oauth: %{
      slug: "oauth",
      name: "SSO",
      setting: :oauth_auth_enabled,
      uid_field: :provider_uid,
      uid_claims: ["sub"],
      email: {:claim, "email_verified", :operator_policy},
      auth_scheme: "Bearer",
      authorize_params: %{},
      lookup: {:get_user_by_provider, ["oauth"]}
    }
  }

  # Display order of the sign-in buttons.
  @button_order [:google, :microsoft, :github, :oauth]

  @slugs Map.new(@providers, fn {provider, %{slug: slug}} -> {slug, provider} end)

  @doc """
  Resolves a URL segment or stored provider (`"github"`) to its atom.
  """
  @spec parse(term()) :: {:ok, provider()} | {:error, :unsupported_provider}
  def parse(slug) do
    case Map.fetch(@slugs, slug) do
      {:ok, provider} -> {:ok, provider}
      :error -> {:error, :unsupported_provider}
    end
  end

  @doc """
  The static description of `provider` (see the moduledoc).
  """
  @spec fetch!(provider()) :: map()
  def fetch!(provider), do: Map.fetch!(@providers, provider)

  @doc """
  The name shown to users. The generic provider's comes from
  `config :tymeslot, :oauth_provider` when set (on Cloudron, the name of the
  Cloudron identity provider), and is "SSO" otherwise.
  """
  @spec name(provider()) :: String.t()
  def name(:oauth) do
    case String.trim(Application.get_env(:tymeslot, :oauth_provider, [])[:name] || "") do
      "" -> fetch!(:oauth).name
      name -> name
    end
  end

  def name(provider), do: fetch!(provider).name

  @doc "The callback path the provider redirects back to."
  @spec callback_path(provider()) :: String.t()
  def callback_path(provider), do: "/auth/#{fetch!(provider).slug}/callback"

  @doc "Whether the admin has switched sign-in with `provider` on."
  @spec enabled?(provider()) :: boolean()
  def enabled?(provider), do: AppSettings.get(fetch!(provider).setting) == true

  @doc """
  The enabled providers in button order, as `%{slug: _, name: _}`.
  """
  @spec enabled() :: [%{slug: String.t(), name: String.t()}]
  def enabled do
    for provider <- @button_order, enabled?(provider) do
      %{slug: fetch!(provider).slug, name: name(provider)}
    end
  end

  @doc """
  Finds the account carrying `uid` as its ID at `provider`.
  """
  @spec find_user(provider(), String.t() | nil, module()) :: {:ok, map()} | {:error, :not_found}
  def find_user(_provider, uid, _repo) when not is_binary(uid) or uid == "",
    do: {:error, :not_found}

  def find_user(provider, uid, repo) do
    {function, leading} = fetch!(provider).lookup
    apply(UserQueries, function, leading ++ [uid, repo])
  end

  @doc """
  Endpoints, scope and client credentials for `provider`. The generic
  provider's come from `config :tymeslot, :oauth_provider`, and raise when a
  required one is missing; an endpoint given as a relative path is resolved
  against the provider's base URL (`:site`).
  """
  @spec config(provider()) :: config()
  def config(:github) do
    %{
      client_id: System.get_env("GITHUB_CLIENT_ID"),
      client_secret: System.get_env("GITHUB_CLIENT_SECRET"),
      authorize_url: "https://github.com/login/oauth/authorize",
      token_url: "https://github.com/login/oauth/access_token",
      userinfo_url: "https://api.github.com/user",
      scope: "user:email"
    }
  end

  def config(:google) do
    %{
      client_id: System.get_env("GOOGLE_CLIENT_ID"),
      client_secret: System.get_env("GOOGLE_CLIENT_SECRET"),
      authorize_url: GoogleEndpoints.authorize_url(),
      token_url: GoogleEndpoints.token_url(),
      userinfo_url: GoogleEndpoints.userinfo_url(),
      scope: "email profile"
    }
  end

  def config(:microsoft) do
    %{
      client_id: MicrosoftConfig.client_id(),
      client_secret: MicrosoftConfig.client_secret(),
      authorize_url: "https://login.microsoftonline.com/common/oauth2/v2.0/authorize",
      token_url: "https://login.microsoftonline.com/common/oauth2/v2.0/token",
      userinfo_url: "https://graph.microsoft.com/oidc/userinfo",
      scope: "openid email profile"
    }
  end

  def config(:oauth) do
    config = Application.get_env(:tymeslot, :oauth_provider, [])
    endpoints = [:authorize_url, :token_url, :userinfo_url]

    case Enum.filter([:client_id, :client_secret | endpoints], &is_nil(config[&1])) do
      [] ->
        config
        |> Keyword.take([:client_id, :client_secret])
        |> Map.new()
        |> Map.merge(Map.new(endpoints, &{&1, absolute_url(config[&1], config[:site])}))
        |> Map.put(:scope, Keyword.get(config, :scope, "openid email profile"))

      missing ->
        raise "Generic OAuth config incomplete, missing keys: #{inspect(missing)}. " <>
                "Set the corresponding OAUTH_* environment variables."
    end
  end

  # An endpoint may be configured as a path relative to the provider's base
  # URL (`OAUTH_PROVIDER_URL`).
  defp absolute_url(url, site) do
    case URI.parse(url) do
      %URI{scheme: nil} when is_binary(site) -> site |> URI.merge(url) |> URI.to_string()
      _absolute_or_no_site -> url
    end
  end
end
