defmodule Tymeslot.Workers.EmailWorkerHandlers do
  @moduledoc """
  Internal handlers for EmailWorker actions.
  """

  alias Tymeslot.Workers.EmailWorkerHandlers.AdminEmails
  alias Tymeslot.Workers.EmailWorkerHandlers.AuthEmails
  alias Tymeslot.Workers.EmailWorkerHandlers.BookingApprovalEmails
  alias Tymeslot.Workers.EmailWorkerHandlers.GuestEmails
  alias Tymeslot.Workers.EmailWorkerHandlers.IntegrationEmails
  alias Tymeslot.Workers.EmailWorkerHandlers.MeetingEmails
  alias Tymeslot.Workers.EmailWorkerHandlers.PollEmails
  alias Tymeslot.Workers.EmailWorkerHandlers.ShareLinkEmails

  # Static dispatch table — keeps `execute_email_action/3` simple and lets
  # adding a new email type be a one-line change. Each entry maps the
  # serialised action name to the function that handles its args. An entry
  # tagged `:with_job_id` also receives the Oban job id, which the handler
  # passes to `Tymeslot.Workers.DeliveryClaims` so that a rescued job does not
  # repeat a send it already made.
  @action_handlers %{
    "send_admin_alert" => {AdminEmails, :handle_admin_alert},
    "send_admin_alert_digest" => {AdminEmails, :handle_admin_alert_digest},
    "send_confirmation_emails" => {MeetingEmails, :handle_confirmation_emails},
    "send_cancellation_emails" => {MeetingEmails, :handle_cancellation_emails, :with_job_id},
    "send_guest_invitations" => {GuestEmails, :handle_guest_invitations},
    "send_reminder_emails" => {MeetingEmails, :handle_reminder_emails},
    "send_reschedule_request" => {MeetingEmails, :handle_reschedule_request},
    "send_booking_request_emails" => {BookingApprovalEmails, :handle_booking_request_emails},
    "send_booking_approval_nudge" => {BookingApprovalEmails, :handle_booking_approval_nudge},
    "send_booking_request_outcome" => {BookingApprovalEmails, :handle_booking_request_outcome},
    "send_reschedule_request_expired" =>
      {BookingApprovalEmails, :handle_reschedule_request_expired},
    "send_poll_deadline_reminders" => {PollEmails, :handle_deadline_reminders, :with_job_id},
    "send_poll_host_nudge" => {PollEmails, :handle_host_nudge, :with_job_id},
    "send_share_links" => {ShareLinkEmails, :handle_share_links, :with_job_id},
    "send_email_change_confirmations" => {AuthEmails, :handle_email_change_confirmations},
    "send_email_verification" => {AuthEmails, :handle_email_verification},
    "send_password_reset" => {AuthEmails, :handle_password_reset},
    "send_no_password_to_reset" => {AuthEmails, :handle_no_password_to_reset},
    "send_signup_attempt_notice" => {AuthEmails, :handle_signup_attempt_notice},
    "send_social_signup_confirmation" => {AuthEmails, :handle_social_signup_confirmation},
    "send_email_change_verification" => {AuthEmails, :handle_email_change_verification},
    "send_email_change_notification" => {AuthEmails, :handle_email_change_notification},
    "send_integration_unhealthy_notification" =>
      {IntegrationEmails, :handle_integration_unhealthy_notification},
    "send_integration_reauth_notification" =>
      {IntegrationEmails, :handle_integration_reauth_notification},
    "send_integration_paused_notification" =>
      {IntegrationEmails, :handle_integration_paused_notification},
    "send_video_room_creation_error_notification" =>
      {IntegrationEmails, :handle_video_room_creation_error_notification},
    "send_calendar_invitation" => {IntegrationEmails, :handle_calendar_invitation},
    "send_event_update_notification" =>
      {IntegrationEmails, :handle_event_update_notification, :with_job_id}
  }

  # The handlers that declare some of their discards an expected end of the
  # job. `AdminEmails` discards nothing of its own.
  @declaring_handlers [
    AuthEmails,
    BookingApprovalEmails,
    GuestEmails,
    IntegrationEmails,
    MeetingEmails,
    PollEmails
  ]

  @doc """
  Whether `reason`, from a discard one of the handlers returned, is an
  expected end of the email job rather than a fault
  (see `Tymeslot.Infrastructure.ExpectedJobOutcome`).
  """
  @spec expected_discard?(term()) :: boolean()
  def expected_discard?(reason),
    do: Enum.any?(@declaring_handlers, & &1.expected_discard?(reason))

  @doc """
  Executes the specified email action with the given arguments.

  This is the primary entry point for the EmailWorker to process various types of
  email jobs, including confirmations, reminders, and authentication emails.

  Returns `:ok` on success, `{:error, reason}` for retriable failures,
  `{:discard, reason}` for fatal errors that shouldn't be retried,
  or `{:snooze, seconds}` if the job should be delayed.

  `job_id` is the id of the Oban job running the action, or `nil` for a job
  that was never persisted (which cannot be rescued, so needs no guard).
  """
  @spec execute_email_action(String.t(), %{String.t() => term()}, integer() | nil) ::
          :ok | {:error, term()} | {:discard, String.t()} | {:snooze, integer()}
  def execute_email_action(action, args, job_id \\ nil) do
    case Map.fetch(@action_handlers, action) do
      {:ok, {module, fun}} -> apply(module, fun, [args])
      {:ok, {module, fun, :with_job_id}} -> apply(module, fun, [args, job_id])
      :error -> {:discard, "Unknown action: #{action}"}
    end
  end
end
