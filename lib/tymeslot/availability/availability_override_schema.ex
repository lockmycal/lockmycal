defmodule Tymeslot.Availability.AvailabilityOverrideSchema do
  @moduledoc """
  Schema for date-specific availability overrides.
  """
  use Ecto.Schema
  import Ecto.Changeset

  alias Tymeslot.Availability.AvailabilityScheduleSchema
  alias Tymeslot.ChangesetValidators.TimeOrder
  alias Tymeslot.Validation.Constraints

  @type t :: %__MODULE__{
          id: integer() | nil,
          schedule_id: integer() | nil,
          date: Date.t() | nil,
          override_type: String.t() | nil,
          start_time: Time.t() | nil,
          end_time: Time.t() | nil,
          reason: String.t() | nil,
          schedule: AvailabilityScheduleSchema.t() | Ecto.Association.NotLoaded.t(),
          inserted_at: DateTime.t() | nil,
          updated_at: DateTime.t() | nil
        }

  @override_types ~w(unavailable custom_hours available)

  schema "availability_overrides" do
    field(:date, :date)
    field(:override_type, :string)
    field(:start_time, :time)
    field(:end_time, :time)
    field(:reason, :string)

    belongs_to(:schedule, AvailabilityScheduleSchema)

    timestamps(type: :utc_datetime)
  end

  @doc false
  @spec changeset(t(), map()) :: Ecto.Changeset.t()
  def changeset(override, attrs) do
    override
    |> cast(attrs, [:schedule_id, :date, :override_type, :start_time, :end_time, :reason])
    |> validate_required([:schedule_id, :date, :override_type])
    |> validate_inclusion(:override_type, @override_types)
    |> validate_times()
    |> validate_reason()
    |> unique_constraint([:schedule_id, :date])
    |> foreign_key_constraint(:schedule_id)
  end

  defp validate_times(changeset) do
    override_type = get_field(changeset, :override_type)

    if override_type == "custom_hours" do
      changeset
      |> validate_required([:start_time, :end_time], message: "are required for custom hours")
      |> TimeOrder.validate_time_order(:start_time, :end_time)
    else
      changeset
    end
  end

  defp validate_reason(changeset) do
    max = Constraints.override_reason_max_length()
    validate_length(changeset, :reason, max: max, message: "must be #{max} characters or less")
  end
end
