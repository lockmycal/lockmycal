defmodule Tymeslot.Availability.WeeklyAvailabilitySchema do
  @moduledoc """
  Schema for weekly availability settings per day of week.
  """
  use Ecto.Schema
  import Ecto.Changeset

  alias Tymeslot.Availability.AvailabilityBreakSchema
  alias Tymeslot.Availability.AvailabilityScheduleSchema
  alias Tymeslot.ChangesetValidators.TimeOrder
  alias Tymeslot.Validation.Constraints

  @type t :: %__MODULE__{
          id: integer() | nil,
          schedule_id: integer() | nil,
          day_of_week: integer() | nil,
          is_available: boolean(),
          start_time: Time.t() | nil,
          end_time: Time.t() | nil,
          max_booked_minutes: pos_integer() | nil,
          schedule: AvailabilityScheduleSchema.t() | Ecto.Association.NotLoaded.t(),
          breaks: [AvailabilityBreakSchema.t()] | Ecto.Association.NotLoaded.t(),
          inserted_at: DateTime.t() | nil,
          updated_at: DateTime.t() | nil
        }

  schema "weekly_availability" do
    field(:day_of_week, :integer)
    field(:is_available, :boolean, default: false)
    field(:start_time, :time)
    field(:end_time, :time)
    field(:max_booked_minutes, :integer)

    belongs_to(:schedule, AvailabilityScheduleSchema)
    has_many(:breaks, AvailabilityBreakSchema, foreign_key: :weekly_availability_id)

    timestamps(type: :utc_datetime)
  end

  @doc false
  @spec changeset(t(), map()) :: Ecto.Changeset.t()
  def changeset(weekly_availability, attrs) do
    weekly_availability
    |> cast(attrs, [
      :schedule_id,
      :day_of_week,
      :is_available,
      :start_time,
      :end_time,
      :max_booked_minutes
    ])
    |> validate_required([:schedule_id, :day_of_week])
    |> validate_inclusion(:day_of_week, 1..7,
      message: "must be between 1 (Monday) and 7 (Sunday)"
    )
    |> validate_times()
    |> validate_inclusion(:max_booked_minutes, Constraints.daily_booked_minutes_values())
    |> check_constraint(:max_booked_minutes,
      name: :weekly_availability_max_booked_minutes_positive
    )
    |> unique_constraint([:schedule_id, :day_of_week])
    |> foreign_key_constraint(:schedule_id)
  end

  defp validate_times(changeset) do
    is_available = get_field(changeset, :is_available)

    if is_available do
      changeset
      |> validate_required([:start_time, :end_time],
        message: "are required when day is available"
      )
      |> TimeOrder.validate_time_order(:start_time, :end_time)
    else
      changeset
    end
  end
end
