defmodule TymeslotWeb.Components.Dashboard.Meetings.Helpers do
  @moduledoc """
  Helpers for meeting display and policy checks in the dashboard.
  """

  alias Tymeslot.Bookings.Policy
  alias Tymeslot.Meetings.Guests
  alias Tymeslot.Utils.DateTimeUtils
  alias Tymeslot.Utils.DateTimeUtils.TimeFormat
  alias TymeslotWeb.Helpers.LocaleFormat

  # Status helpers
  @spec past_meeting?(Ecto.Schema.t()) :: boolean()
  def past_meeting?(meeting) do
    DateTime.compare(meeting.end_time, DateTime.utc_now()) == :lt
  end

  # Policy helpers (surface booleans)
  @spec can_cancel?(Ecto.Schema.t()) :: boolean()
  def can_cancel?(meeting) do
    case Policy.can_cancel_meeting?(meeting) do
      :ok -> true
      {:error, _reason} -> false
    end
  end

  @doc """
  Whether the host may still add guests to this booking: the meeting takes
  guests (`Guests.invitations_open?/1`) and has room for another.

  The meeting type's `allow_guests` is deliberately not consulted — it governs
  what the person booking may do on the public form, not whom the host may
  invite to their own meeting afterwards.

  Counted from the guests preloaded onto the card, so a list of bookings does
  not turn into one query per row.
  """
  @spec can_add_guests?(Ecto.Schema.t() | map()) :: boolean()
  def can_add_guests?(%{guests: guests} = meeting) when is_list(guests),
    do: Guests.invitations_open?(meeting) and length(guests) < Guests.max_guests()

  def can_add_guests?(meeting), do: Guests.invitations_open?(meeting)

  @spec can_reschedule?(Ecto.Schema.t()) :: boolean()
  def can_reschedule?(meeting) do
    case Policy.can_reschedule_meeting?(meeting) do
      :ok -> true
      {:error, _reason} -> false
    end
  end

  # Timezone + formatting helpers
  @spec get_meeting_timezone(Ecto.Schema.t() | nil, Ecto.Schema.t() | nil) :: String.t()
  def get_meeting_timezone(nil, _profile), do: "UTC"
  def get_meeting_timezone(_meeting, nil), do: "UTC"

  def get_meeting_timezone(_meeting, profile) do
    # Organizer's timezone for the dashboard view
    (profile && profile.timezone) || "UTC"
  end

  @spec format_meeting_date(Ecto.Schema.t(), String.t()) :: String.t()
  def format_meeting_date(meeting, timezone) do
    locale = Gettext.get_locale(TymeslotWeb.Gettext)
    local_time = DateTimeUtils.convert_to_timezone(meeting.start_time, timezone)
    LocaleFormat.format_date(local_time, locale)
  end

  @doc """
  Formats a meeting's time range for the organiser's dashboard.

  Takes the clock format explicitly rather than defaulting it: the dashboard is
  organiser-facing and must follow their preference, so a call site that has
  not thought about which clock to use should fail to compile rather than
  quietly pick one.
  """
  @spec format_meeting_time(Ecto.Schema.t(), String.t(), String.t()) :: String.t()
  def format_meeting_time(meeting, timezone, time_format) do
    local_start = DateTimeUtils.convert_to_timezone(meeting.start_time, timezone)
    local_end = DateTimeUtils.convert_to_timezone(meeting.end_time, timezone)

    start_time = TimeFormat.format(local_start, time_format)
    end_time = TimeFormat.format(local_end, time_format)
    "#{start_time} - #{end_time}"
  end

  @doc """
  Formats the date a meeting was cancelled, in the organiser's timezone.

  Returns `nil` for a meeting with no `cancelled_at` (not itself cancelled, or
  cancelled before that field existed), so the caller can decide whether to
  render anything.
  """
  @spec format_cancelled_at_date(Ecto.Schema.t(), String.t()) :: String.t() | nil
  def format_cancelled_at_date(%{cancelled_at: nil}, _timezone), do: nil

  def format_cancelled_at_date(meeting, timezone) do
    locale = Gettext.get_locale(TymeslotWeb.Gettext)
    local_time = DateTimeUtils.convert_to_timezone(meeting.cancelled_at, timezone)
    LocaleFormat.format_date(local_time, locale)
  end

  @doc """
  The date after which the organiser's cancelled-meeting cleanup
  (`Tymeslot.Workers.DeleteCancelledMeetingsWorker`) deletes `meeting`, or
  `nil` when `profile` has the cleanup off or the meeting has no cancellation
  time. `profile` must be the meeting's organiser's — the cleanup runs on
  their setting.
  """
  @spec format_auto_delete_date(map(), map() | nil, String.t()) :: String.t() | nil
  def format_auto_delete_date(
        %{cancelled_at: %DateTime{} = cancelled_at},
        %{
          auto_delete_cancelled_meetings_enabled: true,
          auto_delete_cancelled_meetings_after_days: days
        },
        timezone
      )
      when is_integer(days) do
    locale = Gettext.get_locale(TymeslotWeb.Gettext)

    cancelled_at
    |> DateTime.add(days, :day)
    |> DateTimeUtils.convert_to_timezone(timezone)
    |> LocaleFormat.format_date(locale)
  end

  def format_auto_delete_date(_meeting, _profile, _timezone), do: nil
end
