defmodule TymeslotWeb.Dashboard.CalendarGrid.EventHandlers.NotificationFlows do
  @moduledoc "Attendee-notification flow handlers for CalendarGridComponent."

  use Gettext, backend: TymeslotWeb.Gettext

  import Phoenix.Component, only: [assign: 3]

  alias Tymeslot.Meetings.AttendeeNotifications

  @spec handle_notify_prompt_confirm(map(), Phoenix.LiveView.Socket.t()) ::
          {:noreply, Phoenix.LiveView.Socket.t()}
  def handle_notify_prompt_confirm(_params, socket) do
    case socket.assigns.notify_prompt do
      nil ->
        {:noreply, socket}

      %{kind: :update, scope: scope} = prompt when scope in [:following, :all] ->
        prompt.original_event
        |> AttendeeNotifications.series_updated_confirm(
          prompt.event,
          socket.assigns.current_user.id,
          scope
        )
        |> series_notified(socket)

      %{kind: :update, summary: summary, event: event, attendees: attendees} ->
        case AttendeeNotifications.event_updated_confirm(event, summary, attendees) do
          {:ok, :sent} ->
            send(
              self(),
              {:flash,
               {:info,
                dgettext(
                  "dashboard_calendar_events",
                  "Changes saved. Attendees will be notified shortly."
                )}}
            )

            {:noreply,
             socket
             |> assign(:notify_prompt, nil)
             |> assign(:pending_notification, true)}

          {:error, _reason} ->
            send(
              self(),
              {:flash,
               {:warning,
                dgettext(
                  "dashboard_calendar_events",
                  "Could not schedule notification. Changes were saved."
                )}}
            )

            {:noreply, assign(socket, :notify_prompt, nil)}
        end

      # The cancellation is sent by the delete itself, once the calendar has
      # deleted the event, so a delete that fails notifies nobody.
      %{kind: :delete, event: event, scope: scope} ->
        user_id = socket.assigns.current_user.id

        send(
          self(),
          {:execute_delete_event, build_delete_payload(event, user_id, true, scope)}
        )

        {:noreply,
         socket
         |> assign(:notify_prompt, nil)
         |> assign(:deleting_event, true)}
    end
  end

  # The update of a whole series is sent, not scheduled, so there is no
  # pending notification to show or cancel afterwards.
  defp series_notified({:ok, _sent_or_noop}, socket) do
    send(
      self(),
      {:flash,
       {:info,
        dgettext(
          "dashboard_calendar_events",
          "Changes saved. Attendees will be notified shortly."
        )}}
    )

    {:noreply, assign(socket, :notify_prompt, nil)}
  end

  defp series_notified({:error, _reason}, socket) do
    send(
      self(),
      {:flash,
       {:warning,
        dgettext(
          "dashboard_calendar_events",
          "Could not schedule notification. Changes were saved."
        )}}
    )

    {:noreply, assign(socket, :notify_prompt, nil)}
  end

  @spec handle_notify_prompt_cancel(map(), Phoenix.LiveView.Socket.t()) ::
          {:noreply, Phoenix.LiveView.Socket.t()}
  def handle_notify_prompt_cancel(_params, socket) do
    case socket.assigns.notify_prompt do
      nil ->
        {:noreply, socket}

      %{kind: :update} ->
        send(self(), {:flash, {:info, dgettext("dashboard_calendar_events", "Changes saved.")}})
        {:noreply, assign(socket, :notify_prompt, nil)}

      %{kind: :delete, event: event, scope: scope} ->
        user_id = socket.assigns.current_user.id

        send(
          self(),
          {:execute_delete_event, build_delete_payload(event, user_id, false, scope)}
        )

        {:noreply,
         socket
         |> assign(:notify_prompt, nil)
         |> assign(:deleting_event, true)}
    end
  end

  @spec handle_cancel_pending_notification(map(), Phoenix.LiveView.Socket.t()) ::
          {:noreply, Phoenix.LiveView.Socket.t()}
  def handle_cancel_pending_notification(_params, socket) do
    case socket.assigns.selected_event do
      nil ->
        {:noreply, socket}

      event ->
        :ok = AttendeeNotifications.cancel_pending(event)

        send(
          self(),
          {:flash,
           {:info, dgettext("dashboard_calendar_events", "Pending notification cancelled.")}}
        )

        {:noreply, assign(socket, :pending_notification, false)}
    end
  end

  @doc """
  Builds the payload used to dispatch `{:execute_delete_event, payload}` from
  either the rate-limit-gated confirm path or the notify-prompt branches.
  `scope` is how much of a series the delete takes (see
  `Tymeslot.CalendarGrid.delete_event/3`).
  """
  @spec build_delete_payload(map(), integer(), boolean(), :occurrence | :series) :: map()
  def build_delete_payload(event, user_id, notify_on_delete, scope \\ :occurrence) do
    %{
      uid: event.uid,
      provider_event_id: event.provider_event_id,
      calendar_integration_id: event.calendar_integration_id,
      # Whether the event is one of a series, which keeps its video room.
      recurring_event_id: Map.get(event, :recurring_event_id),
      recurrence_rule: Map.get(event, :recurrence_rule),
      user_id: user_id,
      notify_on_delete: notify_on_delete,
      scope: scope
    }
  end
end
