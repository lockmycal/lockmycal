defmodule TymeslotWeb.Dashboard.CalendarGrid.Views.AllDayRow do
  @moduledoc "All-day cell function component for the calendar grid week/day view."

  use TymeslotWeb, :html
  use Gettext, backend: TymeslotWeb.Gettext

  alias TymeslotWeb.Components.Dashboard.Meetings.AttendeeAttachments
  alias TymeslotWeb.Dashboard.CalendarGrid.Helpers

  @allday_visible_limit 2

  # ---------- All-day cell (with cap + "more" disclosure) ----------

  attr :assigns_ref, :map, required: true
  attr :day, :any, required: true
  attr :myself, :any, required: true

  @spec all_day_cell(map()) :: Phoenix.LiveView.Rendered.t()
  def all_day_cell(assigns) do
    all_day_events = Helpers.all_day_events_for_day(assigns.assigns_ref, assigns.day)
    {shown, hidden} = Enum.split(all_day_events, @allday_visible_limit)

    assigns =
      assigns
      |> assign(:shown, shown)
      |> assign(:hidden, hidden)
      |> assign(:hidden_count, length(hidden))

    ~H"""
    <details class={"group border-l border-neutral-300 dark:border-twilight-indigo-800 p-0.5 min-w-0 min-h-[1.5rem] [&>summary::-webkit-details-marker]:hidden #{Helpers.day_column_class(@day, @assigns_ref)}"}>
      <summary class="flex flex-col gap-0.5 list-none cursor-default">
        <%!-- An all-day event covering several days is rendered in each of
              its cells, so the id names the day as well. --%>
        <div
          :for={event <- @shown}
          id={"allday-event-#{event.id}-#{@day}"}
          phx-hook="StopClickPropagation"
          class={"rounded px-1 text-token-xs truncate cursor-pointer #{Helpers.event_block_class(@assigns_ref, event)}"}
          phx-click="show_event"
          phx-value-event-id={event.id}
          phx-target={@myself}
          role="button"
          tabindex="0"
          aria-label={
            dgettext("dashboard_calendar", "All-day: %{event}",
              event: event.summary || dgettext("dashboard_calendar", "Untitled event")
            )
          }
        >
          <img
            :if={Map.get(event, :created_by_tymeslot)}
            src="/images/brand/logo.svg"
            alt=""
            class="inline-block w-3 h-3 opacity-60 mr-0.5 align-text-bottom"
          /><.icon
            :if={(Map.get(event, :reminders) || []) != []}
            name="hero-bell-micro"
            class="inline-block w-3 h-3 opacity-70 mr-0.5 align-text-bottom"
          /><AttendeeAttachments.marker attachments={Map.get(event, :attendee_attachments)} />{event.summary ||
            dgettext("dashboard_calendar", "(No title)")}
        </div>
        <span
          :if={@hidden_count > 0}
          class="text-token-xs text-neutral-500 hover:text-neutral-700 dark:text-neutral-200 cursor-pointer px-1 group-open:hidden"
        >{dngettext("dashboard_calendar", "+%{count} more", "+%{count} more", @hidden_count,
          count: @hidden_count
        )}</span>
      </summary>
      <div :if={@hidden_count > 0} class="flex flex-col gap-0.5 mt-0.5">
        <div
          :for={event <- @hidden}
          class={"rounded px-1 text-token-xs truncate cursor-pointer #{Helpers.event_block_class(@assigns_ref, event)}"}
          phx-click="show_event"
          phx-value-event-id={event.id}
          phx-target={@myself}
          role="button"
          tabindex="0"
        >
          {event.summary || dgettext("dashboard_calendar", "(No title)")}
        </div>
      </div>
    </details>
    """
  end
end
