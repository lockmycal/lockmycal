defmodule TymeslotWeb.Live.Dashboard.EmbedSettings.ComponentsTest do
  use TymeslotWeb.ConnCase, async: true

  @moduletag :components
  @moduletag :security

  import Phoenix.LiveViewTest
  alias TymeslotWeb.Live.Dashboard.EmbedSettings.LivePreview
  alias TymeslotWeb.Live.Dashboard.EmbedSettings.OptionsGrid
  alias TymeslotWeb.Live.Dashboard.EmbedSettings.SecuritySection

  describe "OptionsGrid component" do
    @base_grid_assigns %{
      username: "testuser",
      base_url: "https://tymeslot.com",
      booking_url: "https://tymeslot.com/testuser",
      myself: "myself"
    }

    test "renders all options" do
      assigns = Map.put(@base_grid_assigns, :selected_embed_type, "inline")

      html = render_component(&OptionsGrid.options_grid/1, assigns)
      assert html =~ "Inline Embed"
      assert html =~ "Popup Modal"
      assert html =~ "Direct Link"
      assert html =~ "Floating Button"
      assert html =~ "Recommended"
    end

    test "defaults to column layout when embed_layout is not supplied" do
      # Confirms the attr default matches the product default set in
      # EmbedSettingsComponent (assign_new :embed_layout → "column"). The link
      # snippet opens the standalone booking page, which defaults to :default,
      # so a column choice emits ?layout=column. (Embed snippets carry their
      # column intent via data-layout="column" / layout: 'column' instead —
      # asserted separately in embed_settings_test.exs.)
      assigns = Map.put(@base_grid_assigns, :selected_embed_type, "link")

      html = render_component(&OptionsGrid.options_grid/1, assigns)

      assert html =~ "?layout=column"
    end

    test "exactly one card carries the selection indicator at any time" do
      # data-selected="true" is set on the card container only when selected.
      # Regardless of which type is active, exactly one card should be marked.
      for selected_type <- ["inline", "popup", "link", "floating"] do
        assigns = Map.put(@base_grid_assigns, :selected_embed_type, selected_type)
        html = render_component(&OptionsGrid.options_grid/1, assigns)

        selected_count =
          html
          |> String.split(~s(data-selected="true"))
          |> length()
          |> Kernel.-(1)

        assert selected_count == 1,
               "Expected exactly one selected card for type=#{selected_type}, " <>
                 "but found #{selected_count} cards with data-selected=true"
      end
    end
  end

  describe "SecuritySection component" do
    test "renders security section" do
      assigns = %{
        allowed_domains: [],
        myself: "myself"
      }

      html = render_component(&SecuritySection.security_section/1, assigns)
      assert html =~ "Security &amp; Domain Control"
      assert html =~ "Add Allowed Domain"
      assert html =~ "Disabled"
    end

    test "renders when restricted" do
      assigns = %{
        allowed_domains: ["example.com"],
        myself: "myself"
      }

      html = render_component(&SecuritySection.security_section/1, assigns)
      assert html =~ "Security &amp; Domain Control"
      assert html =~ "Add Allowed Domain"
      assert html =~ "example.com"
      assert html =~ "Restricted"
    end

    test "renders when disabled with none" do
      assigns = %{
        allowed_domains: ["none"],
        myself: "myself"
      }

      html = render_component(&SecuritySection.security_section/1, assigns)
      assert html =~ "Security &amp; Domain Control"
      assert html =~ "Disabled"
    end
  end

  describe "LivePreview component" do
    test "renders readiness warning when not ready" do
      assigns = %{
        show_preview: true,
        selected_embed_type: "inline",
        username: "testuser",
        base_url: "https://tymeslot.com",
        preview_token: "a-signed-owner-preview-token",
        embed_script_url: "/embed.js",
        is_ready: false,
        error_reason: :no_calendar,
        myself: "myself"
      }

      html = render_component(&LivePreview.live_preview/1, assigns)
      assert html =~ "Link Deactivated"
      assert html =~ "The organizer hasn’t connected a calendar yet."
    end

    test "renders preview container" do
      assigns = %{
        show_preview: true,
        selected_embed_type: "inline",
        username: "testuser",
        base_url: "https://tymeslot.com",
        preview_token: "a-signed-owner-preview-token",
        embed_script_url: "/embed.js",
        is_ready: true,
        error_reason: nil,
        myself: "myself"
      }

      html = render_component(&LivePreview.live_preview/1, assigns)
      assert html =~ "id=\"live-preview-container\""
      assert html =~ "data-username=\"testuser\""

      # The hook copies this into the iframe's ?preview_token=, which is what
      # lets the owner's test booking simulate instead of being refused.
      assert html =~ "data-preview-token=\"a-signed-owner-preview-token\""
    end

    # The hook writes no wording of its own. The snippet labels stand in for the
    # copied snippet, so they follow the embed's language; the rest is dashboard
    # chrome and follows the viewer's.
    test "hands the hook translated text, labels in the embed's language" do
      Gettext.put_locale(TymeslotWeb.Gettext, "de")
      on_exit(fn -> Gettext.put_locale(TymeslotWeb.Gettext, "en") end)

      assigns = %{
        selected_embed_type: "link",
        username: "testuser",
        base_url: "https://tymeslot.com",
        preview_token: "a-signed-owner-preview-token",
        embed_script_url: "/embed.js",
        embed_locale: "fr",
        is_ready: true,
        error_reason: nil,
        myself: "myself"
      }

      html = render_component(&LivePreview.live_preview/1, assigns)

      assert html =~ ~s(data-link-label="Planifier une réunion")
      assert html =~ ~s(data-popup-label="Réserver une réunion")
      assert html =~ ~s(data-link-hint="Nur Vorschau: Dieser Link öffnet sich im Testmodus)
      assert html =~ ~s(data-iframe-title="Buchungsvorschau")
    end
  end
end
