defmodule TymeslotWeb.Dashboard.MeetingSettings.MeetingTypeForm.SubmissionTest do
  use ExUnit.Case, async: true

  @moduletag :meeting_types

  alias Tymeslot.MeetingTypes.LocationOption
  alias TymeslotWeb.Dashboard.MeetingSettings.MeetingTypeForm.Submission

  defp base_assigns(overrides) do
    Map.merge(
      %{
        form_data: %{
          "name" => "Quick Chat",
          "duration" => "30",
          "slot_interval" => "15",
          "description" => "Hi"
        },
        type: %{is_active: true},
        selected_icon: "hero-bolt",
        venues: [%{id: 1}, %{id: 3}],
        locations: [
          %LocationOption{
            id: "loc-1",
            kind: "in_person",
            label: "The office",
            venue_ids: [3, 1],
            position: 0
          }
        ],
        selected_calendar_integration_id: 7,
        selected_target_calendar_id: "cal-1",
        reminders: [%{value: 30, unit: "minutes"}],
        custom_fields: [],
        custom_questions_allowed: true,
        payments_feature_enabled: false,
        payments_charges_enabled: false,
        payment_required: false,
        payment_price: ""
      },
      overrides
    )
  end

  describe "build_params/1" do
    test "serialises socket assigns into the meeting_type params map" do
      params = Submission.build_params(base_assigns(%{}))

      assert params["name"] == "Quick Chat"
      assert params["duration"] == "30"
      assert params["slot_interval"] == "15"
      assert params["description"] == "Hi"
      assert params["icon"] == "hero-bolt"
      assert params["is_active"] == "true"
      assert params["calendar_integration_id"] == "7"
      assert params["target_calendar_id"] == "cal-1"
      assert params["reminder_config"] == [%{"value" => "30", "unit" => "minutes"}]
      assert params["custom_fields"] == []
    end

    test "represents a missing slot_interval as an empty string" do
      params =
        Submission.build_params(
          base_assigns(%{
            form_data: %{"name" => "Quick Chat", "duration" => "30", "description" => "Hi"}
          })
        )

      assert params["slot_interval"] == ""
    end

    test "represents an unset integration id as an empty string" do
      params = Submission.build_params(base_assigns(%{selected_calendar_integration_id: nil}))

      assert params["calendar_integration_id"] == ""
    end

    test "serialises locations, omitting the integration id a non-video one has not got" do
      params = Submission.build_params(base_assigns(%{}))

      assert [location] = params["locations"]
      assert location["id"] == "loc-1"
      assert location["kind"] == "in_person"
      assert location["label"] == "The office"
      assert location["details"] == ""
      assert location["venue_ids"] == ["3", "1"]
      assert location["collect_from_guest"] == "false"
      assert location["position"] == "0"
      assert location["video_integration_ids"] == []
    end

    test "drops a venue that is no longer among the host's locations" do
      params = Submission.build_params(base_assigns(%{venues: [%{id: 1}]}))

      assert [%{"venue_ids" => ["1"]}] = params["locations"]
    end

    test "carries every integration a video location offers, in order" do
      params =
        Submission.build_params(
          base_assigns(%{
            locations: [
              %LocationOption{
                id: "loc-2",
                kind: "video",
                label: "Zoom",
                video_integration_ids: [9, 4],
                position: 0
              }
            ]
          })
        )

      assert [%{"kind" => "video", "video_integration_ids" => ["9", "4"]}] = params["locations"]
    end

    test "omits custom_fields entirely when custom questions are not allowed" do
      params =
        Submission.build_params(
          base_assigns(%{
            custom_questions_allowed: false,
            custom_fields: [%{id: "x", type: "short_text", label: "Co"}]
          })
        )

      refute Map.has_key?(params, "custom_fields")
    end

    test "includes payment fields only when charges are enabled" do
      params =
        Submission.build_params(
          base_assigns(%{
            payments_feature_enabled: true,
            payments_charges_enabled: true,
            payment_required: true,
            payment_price: "12.00"
          })
        )

      assert params["payment_required"] == "true"
      assert params["price"] == "12.00"
    end

    test "omits payment fields when the host cannot accept charges" do
      params = Submission.build_params(base_assigns(%{payments_feature_enabled: true}))

      refute Map.has_key?(params, "payment_required")
      refute Map.has_key?(params, "price")
    end

    test "omits translations entirely when the assign is absent" do
      params = Submission.build_params(base_assigns(%{}))

      refute Map.has_key?(params, "translations")
    end

    test "omits translations entirely when the list is empty" do
      params = Submission.build_params(base_assigns(%{translations: []}))

      refute Map.has_key?(params, "translations")
    end

    test "serialises translations into the meeting_type params map" do
      params =
        Submission.build_params(
          base_assigns(%{
            translations: [
              %{id: "t1", locale: "de", name: "Kurzes Gespräch", description: nil}
            ]
          })
        )

      assert params["translations"] == [
               %{"id" => "t1", "locale" => "de", "name" => "Kurzes Gespräch", "description" => ""}
             ]
    end
  end
end
