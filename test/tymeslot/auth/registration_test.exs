defmodule Tymeslot.Auth.RegistrationTest do
  use Tymeslot.DataCase, async: true

  @moduletag :auth

  import Tymeslot.Test.AdminBootstrapHelpers, only: [reopen_admin_bootstrap: 1]

  alias Tymeslot.Auth.{Registration, UserSchema}
  alias Tymeslot.Repo
  alias TymeslotWeb.Helpers.ClientIP
  import Tymeslot.Factory

  describe "registration security" do
    test "enforces strong password requirements" do
      conn = ClientIP.request_opts(%Plug.Conn{})

      weak_passwords = [
        # Too short
        "short",
        # No letters
        "12345678",
        # No numbers
        "password",
        # No special chars
        "Password1"
      ]

      Enum.each(weak_passwords, fn password ->
        params = %{
          "email" => "test#{System.unique_integer([:positive])}@example.com",
          "password" => password,
          "password_confirmation" => password,
          "name" => "Test"
        }

        assert {:error, :input, _changeset} = Registration.register_user(params, conn)
      end)
    end

    test "prevents duplicate accounts (case-insensitive)" do
      conn = ClientIP.request_opts(%Plug.Conn{})
      insert(:user, email: "existing@example.com")

      params = %{
        "email" => "EXISTING@EXAMPLE.COM",
        "password" => "ValidPassword123!",
        "password_confirmation" => "ValidPassword123!",
        "name" => "Duplicate",
        "terms_accepted" => "true"
      }

      # Answered exactly as a free address would be; the owner is told by email.
      assert {:existing_account, _message} = Registration.register_user(params, conn)
      assert Repo.aggregate(UserSchema, :count, :id) == 1
    end

    test "new accounts require email verification" do
      conn = ClientIP.request_opts(%Plug.Conn{})

      params = %{
        "email" => "new@example.com",
        "password" => "ValidPassword123!",
        "password_confirmation" => "ValidPassword123!",
        "name" => "New User",
        "terms_accepted" => "true"
      }

      {:ok, user, _conn} = Registration.register_user(params, conn)
      assert is_nil(user.verified_at)
    end
  end

  describe "oauth registration security" do
    test "oauth accounts cannot re-register with passwords" do
      oauth_user = insert(:user, email: "oauth@gmail.com", provider: "google", password_hash: nil)

      params = %{
        "email" => oauth_user.email,
        "password" => "ValidPassword123!",
        "password_confirmation" => "ValidPassword123!",
        "name" => "OAuth User",
        "terms_accepted" => "true"
      }

      assert {:existing_account, _message} =
               Registration.register_user(params, ClientIP.request_opts(%Plug.Conn{}))

      assert Repo.aggregate(UserSchema, :count, :id) == 1
    end
  end

  describe "input sanitization" do
    test "trims the email; a name in the signup params is never persisted at all" do
      # Not an XSS/sanitisation assertion for `name`: create_user/1
      # hardcodes its attrs to email/password/terms, so `user.name` is nil
      # for every signup regardless of input. The `<script>` payload here is
      # a deliberate negative control — it proves nothing is silently
      # smuggled through an unvalidated key, not that anything is sanitised.
      params = %{
        "email" => "  safe@example.com  ",
        "password" => "ValidPassword123!",
        "password_confirmation" => "ValidPassword123!",
        "name" => "<script>alert('xss')</script>Safe Name",
        "terms_accepted" => "true"
      }

      {:ok, user, _session} =
        Registration.register_user(params, ClientIP.request_opts(%Plug.Conn{}))

      assert user.email == "safe@example.com"
      assert is_nil(user.name)
    end
  end

  describe "admin bootstrap" do
    setup :reopen_admin_bootstrap

    test "the first registered user is promoted to admin" do
      conn = ClientIP.request_opts(%Plug.Conn{})

      params = %{
        "email" => "first@example.com",
        "password" => "ValidPassword123!",
        "password_confirmation" => "ValidPassword123!",
        "name" => "First User",
        "terms_accepted" => "true"
      }

      assert {:ok, user, _message} = Registration.register_user(params, conn)
      assert user.is_admin, "Expected the first registered user to be promoted to admin"
    end

    test "a second registered user is not promoted to admin" do
      conn = ClientIP.request_opts(%Plug.Conn{})

      first_params = %{
        "email" => "first2@example.com",
        "password" => "ValidPassword123!",
        "password_confirmation" => "ValidPassword123!",
        "name" => "First User",
        "terms_accepted" => "true"
      }

      {:ok, _first, _flash} = Registration.register_user(first_params, conn)

      second_params = %{
        "email" => "second@example.com",
        "password" => "ValidPassword123!",
        "password_confirmation" => "ValidPassword123!",
        "name" => "Second User",
        "terms_accepted" => "true"
      }

      assert {:ok, second, _message} = Registration.register_user(second_params, conn)
      refute second.is_admin, "Expected the second registered user not to be promoted to admin"
    end
  end

  describe "password storage" do
    test "passwords are hashed before storage" do
      plain = "SecurePassword123!"

      params = %{
        "email" => "secure@example.com",
        "password" => plain,
        "password_confirmation" => plain,
        "name" => "User",
        "terms_accepted" => "true"
      }

      {:ok, user, _session} =
        Registration.register_user(params, ClientIP.request_opts(%Plug.Conn{}))

      # Never store plaintext
      refute user.password_hash == plain
      assert String.starts_with?(user.password_hash, "$2b$")
    end
  end
end
