defmodule Tymeslot.Integrations.Calendar.Orchestration.WorkflowsTest do
  use Tymeslot.DataCase, async: true
  @moduletag :integrations

  alias Tymeslot.Integrations.Calendar.CalendarEntry
  alias Tymeslot.Integrations.Calendar.Orchestration.Workflows
  alias Tymeslot.Integrations.CalendarManagement
  import Tymeslot.Factory
  import Mox

  setup :verify_on_exit!

  describe "refresh_calendar_list_async/3" do
    test "successfully refreshes calendar list and notifies parent" do
      user = insert(:user)
      integration = insert(:calendar_integration, user: user, provider: "google")
      component_id = "comp_123"

      expect(GoogleCalendarAPIMock, :list_calendars, fn _client ->
        {:ok, [%{"id" => "cal_1", "summary" => "New Calendar"}]}
      end)

      {:ok, _pid} = Workflows.refresh_calendar_list_async(integration.id, user.id, component_id)

      assert_receive {:calendar_list_refreshed, ^component_id, id, calendars}, 5000
      assert id == integration.id
      assert [%CalendarEntry{id: "cal_1", name: "New Calendar"}] = calendars

      # Verify DB was updated
      {:ok, updated} = CalendarManagement.get_calendar_integration(integration.id, user.id)
      assert length(updated.calendar_list) == 1
    end

    test "preserves the user's prior selection when refreshing" do
      user = insert(:user)
      kept = "/dav/user@example.org/Calendar/"

      integration =
        insert(:calendar_integration,
          user: user,
          provider: "google",
          calendar_paths: [kept],
          calendar_list: [
            %{"id" => kept, "path" => kept, "name" => "Kept", "selected" => true}
          ]
        )

      component_id = "comp_keep"

      expect(GoogleCalendarAPIMock, :list_calendars, fn _client ->
        {:ok, [%{"id" => kept, "summary" => "Kept"}, %{"id" => "new", "summary" => "New"}]}
      end)

      {:ok, _pid} = Workflows.refresh_calendar_list_async(integration.id, user.id, component_id)

      assert_receive {:calendar_list_refreshed, ^component_id, _, _calendars}, 5000

      {:ok, updated} = CalendarManagement.get_calendar_integration(integration.id, user.id)

      assert Enum.find(updated.calendar_list, &(&1.id == kept)).selected == true
      assert Enum.find(updated.calendar_list, &(&1.id == "new")).selected == false
      assert updated.calendar_paths == [kept]
    end

    test "handles discovery error by returning existing list" do
      user = insert(:user)
      existing_list = [%{id: "old", name: "Old"}]

      integration =
        insert(:calendar_integration,
          user: user,
          provider: "google",
          calendar_list: existing_list
        )

      component_id = "comp_err"

      expect(GoogleCalendarAPIMock, :list_calendars, fn _client ->
        {:error, :api_error}
      end)

      {:ok, _pid} = Workflows.refresh_calendar_list_async(integration.id, user.id, component_id)

      assert_receive {:calendar_list_refreshed, ^component_id, _, calendars}, 5000
      # Should return existing list on error
      assert length(calendars) == 1
      assert List.first(calendars).id == "old"
    end
  end

  describe "update_integration_with_discovery/1" do
    test "merges discovery with existing selection" do
      user = insert(:user)

      integration =
        insert(:calendar_integration,
          user: user,
          provider: "google",
          calendar_list: [%{"id" => "cal_1", "selected" => true, "path" => "p1"}]
        )

      expect(GoogleCalendarAPIMock, :list_calendars, fn _client ->
        {:ok,
         [%{"id" => "cal_1", "summary" => "Cal 1"}, %{"id" => "cal_2", "summary" => "Cal 2"}]}
      end)

      assert {:ok, updated} = Workflows.update_integration_with_discovery(integration)

      # cal_1 should still be selected, cal_2 should be added but not selected
      assert [c1, c2] = Enum.sort_by(updated.calendar_list, & &1.id)
      assert c1.id == "cal_1"
      assert c1.selected == true
      assert c2.id == "cal_2"
      assert c2.selected == false

      # calendar_paths must be written atomically alongside calendar_list;
      # cal_1 is selected and discovery emits no explicit path so it falls back to id
      assert updated.calendar_paths == ["cal_1"]
    end

    test "preserves existing list if discovery returns empty" do
      user = insert(:user)
      existing = [%{"id" => "cal_1", "selected" => true}]

      integration =
        insert(:calendar_integration, user: user, provider: "google", calendar_list: existing)

      expect(GoogleCalendarAPIMock, :list_calendars, fn _client ->
        {:ok, []}
      end)

      assert {:ok, updated} = Workflows.update_integration_with_discovery(integration)
      assert updated.calendar_list == [%CalendarEntry{id: "cal_1", selected: true}]

      # calendar_paths must be written atomically: cal_1 is selected and has no
      # explicit path in the fixture, so derive_selected_paths falls back to its id
      assert updated.calendar_paths == ["cal_1"]
    end

    test "preserves the existing selection when discovery matches none of it" do
      user = insert(:user)

      integration =
        insert(:calendar_integration,
          user: user,
          provider: "google",
          calendar_list: [%{"id" => "cal_1", "selected" => true, "path" => "p1"}]
        )

      # The server now reports the calendar under a different id, so the merge
      # would deselect everything and leave the integration syncing nothing.
      expect(GoogleCalendarAPIMock, :list_calendars, fn _client ->
        {:ok, [%{"id" => "cal_renamed", "summary" => "Renamed"}]}
      end)

      assert {:ok, updated} = Workflows.update_integration_with_discovery(integration)

      assert updated.calendar_paths == ["p1"]
      assert [%CalendarEntry{id: "cal_1", selected: true}] = updated.calendar_list
    end
  end
end
