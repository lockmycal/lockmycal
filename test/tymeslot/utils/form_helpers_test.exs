defmodule Tymeslot.Utils.FormHelpersTest do
  use ExUnit.Case, async: true

  @moduletag :utils

  alias Tymeslot.Utils.FormHelpers

  # Dummy schema for testing changesets
  defmodule Item do
    use Ecto.Schema
    import Ecto.Changeset

    embedded_schema do
      field :title, :string
      field :count, :integer
    end

    @type t :: %__MODULE__{
            title: String.t() | nil,
            count: integer() | nil
          }

    @spec changeset(t(), map()) :: Ecto.Changeset.t()
    def changeset(item, attrs) do
      item
      |> cast(attrs, [:title, :count])
      |> validate_required([:title])
      |> validate_number(:count, greater_than: 0)
    end
  end

  describe "format_changeset_errors/1" do
    test "formats changeset errors into a map of field -> messages" do
      changeset = Item.changeset(%Item{}, %{title: "", count: 0})

      errors = FormHelpers.format_changeset_errors(changeset)

      assert errors == %{
               title: ["can't be blank"],
               count: ["must be greater than 0"]
             }
    end

    test "returns empty map for valid changeset" do
      changeset = Item.changeset(%Item{}, %{title: "Valid Title"})
      assert FormHelpers.format_changeset_errors(changeset) == %{}
    end
  end

  describe "format_context_error/1" do
    test "formats known error atoms" do
      assert FormHelpers.format_context_error(:video_integration_required) ==
               %{video_integration: ["Please select a video provider for video meetings"]}

      assert FormHelpers.format_context_error(:invalid_duration) ==
               %{duration: ["Duration must be a valid number"]}
    end

    test "formats target calendar errors" do
      assert FormHelpers.format_context_error(:target_calendar_required) ==
               %{target_calendar: ["Please select a target calendar"]}

      assert FormHelpers.format_context_error(:target_calendar_invalid) ==
               %{target_calendar: ["Selected calendar is not available for this account"]}
    end

    test "formats no_writable_calendars with an explanation and a way forward" do
      assert %{target_calendar: [message]} =
               FormHelpers.format_context_error(:no_writable_calendars)

      assert message =~ "None of the calendars you selected for this account can accept bookings"
    end

    test "formats invalid approval window against its own field, not base" do
      assert FormHelpers.format_context_error(:invalid_approval_window) ==
               %{
                 approval_window_hours: [
                   "Enter a whole number of hours, or leave blank to use the default."
                 ]
               }
    end

    test "formats generic atoms to capitalized base error" do
      assert FormHelpers.format_context_error(:something_went_wrong) ==
               %{base: ["Something went wrong"]}
    end

    test "formats non-atom errors to string base error" do
      assert FormHelpers.format_context_error("string error") ==
               %{base: ["string error"]}
    end
  end
end
