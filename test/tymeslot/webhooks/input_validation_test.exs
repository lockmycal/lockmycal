defmodule Tymeslot.Webhooks.InputValidationTest do
  use Tymeslot.DataCase, async: false

  @moduletag :security

  alias Tymeslot.Security.RateLimiter
  alias Tymeslot.Webhooks.InputValidation

  setup do
    RateLimiter.clear_all()
    :ok
  end

  describe "validate_webhook_form/2" do
    test "accepts valid webhook params" do
      params = %{
        "name" => "My Webhook",
        "url" => "https://example.com/webhook",
        "events" => ["meeting.created"]
      }

      assert {:ok, validated} = InputValidation.validate_webhook_form(params)
      assert validated.name == "My Webhook"
      assert validated.url == "https://example.com/webhook"
      assert validated.events == ["meeting.created"]
    end

    test "accepts multiple valid events" do
      params = %{
        "name" => "My Webhook",
        "url" => "https://example.com/hook",
        "events" => ["meeting.created", "meeting.cancelled", "meeting.rescheduled"]
      }

      assert {:ok, validated} = InputValidation.validate_webhook_form(params)
      assert length(validated.events) == 3
    end

    test "accepts empty events list" do
      params = %{
        "name" => "My Webhook",
        "url" => "https://example.com/hook",
        "events" => []
      }

      assert {:ok, _validated} = InputValidation.validate_webhook_form(params)
    end

    test "rejects missing name" do
      params = %{"url" => "https://example.com/hook", "events" => []}
      assert {:error, errors} = InputValidation.validate_webhook_form(params)
      assert Map.has_key?(errors, :name)
    end

    test "rejects missing url" do
      params = %{"name" => "My Webhook", "events" => []}
      assert {:error, errors} = InputValidation.validate_webhook_form(params)
      assert Map.has_key?(errors, :url)
    end

    test "rejects invalid url format" do
      params = %{
        "name" => "My Webhook",
        "url" => "not-a-url",
        "events" => []
      }

      assert {:error, errors} = InputValidation.validate_webhook_form(params)
      assert Map.has_key?(errors, :url)
    end

    test "rejects non-https url" do
      params = %{
        "name" => "My Webhook",
        "url" => "ftp://example.com/hook",
        "events" => []
      }

      assert {:error, errors} = InputValidation.validate_webhook_form(params)
      assert Map.has_key?(errors, :url)
    end

    test "rejects invalid event names" do
      params = %{
        "name" => "My Webhook",
        "url" => "https://example.com/hook",
        "events" => ["meeting.created", "invalid.event"]
      }

      assert {:error, errors} = InputValidation.validate_webhook_form(params)
      assert Map.has_key?(errors, :events)
    end

    test "preserves angle-bracket and quote characters in name verbatim" do
      # Webhook names are free-form text. XSS is prevented at render time by
      # Phoenix template auto-escaping; SQL injection is prevented by Ecto
      # parameterised queries. The input validator does not strip these
      # symbols so legitimate names like "Luka <> Paul" or
      # "Webhook 'Production'" round-trip unchanged.
      params = %{
        "name" => "<script>alert(1)</script>My Webhook",
        "url" => "https://example.com/hook",
        "events" => []
      }

      assert {:ok, validated} = InputValidation.validate_webhook_form(params)
      assert validated.name == "<script>alert(1)</script>My Webhook"
    end

    test "preserves SQL-comment punctuation in name verbatim" do
      params = %{
        "name" => "Webhook'; DROP TABLE webhooks; --",
        "url" => "https://example.com/hook",
        "events" => []
      }

      assert {:ok, validated} = InputValidation.validate_webhook_form(params)
      assert validated.name == "Webhook'; DROP TABLE webhooks; --"
    end

    test "preserves percent-encoded url unchanged" do
      params = %{
        "name" => "My Webhook",
        "url" => "https://example.com/hook?q=hello%20world",
        "events" => []
      }

      assert {:ok, validated} = InputValidation.validate_webhook_form(params)
      assert validated.url == "https://example.com/hook?q=hello%20world"
    end

    test "returns error when name is non-string type" do
      params = %{"name" => 42, "url" => "https://example.com/hook", "events" => []}
      assert {:error, errors} = InputValidation.validate_webhook_form(params)
      assert Map.has_key?(errors, :name)
    end

    test "returns error when url is non-string type" do
      params = %{"name" => "My Webhook", "url" => ["https://example.com"], "events" => []}
      assert {:error, errors} = InputValidation.validate_webhook_form(params)
      assert Map.has_key?(errors, :url)
    end

    test "trims whitespace-only name and rejects it" do
      params = %{"name" => "   ", "url" => "https://example.com/hook", "events" => []}
      assert {:error, errors} = InputValidation.validate_webhook_form(params)
      assert Map.has_key?(errors, :name)
    end

    test "strips null bytes from url" do
      # PostgreSQL rejects null bytes even though they are valid UTF-8, so an
      # unsanitised NUL in the URL would reach the changeset and raise a
      # Postgrex error (22021) on insert instead of failing validation here.
      params = %{
        "name" => "My Webhook",
        "url" => "https://example.com/hook?x=a\x00b",
        "events" => []
      }

      assert {:ok, validated} = InputValidation.validate_webhook_form(params)
      refute validated.url =~ "\x00"
      assert validated.url == "https://example.com/hook?x=ab"
    end
  end

  # `Tymeslot.Security.RateLimiter.Integrations` states the rule: never one
  # instance-wide bucket, the actor is always required. A bare literal key gave
  # every logged-in user of an instance the same 60/min budget.
  describe "validate_webhook_form/2 rate limiting" do
    @valid_params %{
      "name" => "My Webhook",
      "url" => "https://example.com/webhook",
      "events" => ["meeting.created"]
    }

    test "one user exhausting the budget does not refuse another user" do
      for _attempt <- 1..61 do
        InputValidation.validate_webhook_form(@valid_params, metadata: %{user_id: 1})
      end

      assert {:error, :rate_limited} =
               InputValidation.validate_webhook_form(@valid_params, metadata: %{user_id: 1})

      assert {:ok, _validated} =
               InputValidation.validate_webhook_form(@valid_params, metadata: %{user_id: 2})
    end

    # Not a field error map: a throttled request says nothing about any field,
    # and the `:form` key it used to return is one the blur handler looks past
    # (deleting the field's real error) and the modal never renders.
    test "refuses with a reason the web layer can act on" do
      for _attempt <- 1..61 do
        InputValidation.validate_webhook_form(@valid_params, metadata: %{user_id: 3})
      end

      assert {:error, :rate_limited} =
               InputValidation.validate_webhook_form(@valid_params, metadata: %{user_id: 3})
    end
  end
end
