defmodule Tymeslot.Bookings.PolicyTest do
  use Tymeslot.DataCase, async: true
  @moduletag :bookings

  import Tymeslot.Factory
  import Mox

  setup :verify_on_exit!

  alias Tymeslot.Bookings.BuildParams
  alias Tymeslot.Bookings.Policy
  alias Tymeslot.Meetings.MeetingSchema
  alias Tymeslot.MeetingTypes

  describe "can_cancel_meeting?/1" do
    test "allows cancellation for future meetings" do
      future_meeting = %MeetingSchema{
        uid: "test-uid",
        status: "confirmed",
        # 1 hour from now
        start_time: DateTime.add(DateTime.utc_now(), 3600, :second),
        # 2 hours from now
        end_time: DateTime.add(DateTime.utc_now(), 7200, :second)
      }

      assert Policy.can_cancel_meeting?(future_meeting) == :ok
    end

    test "blocks cancellation for meetings that have started" do
      current_meeting = %MeetingSchema{
        uid: "test-uid",
        status: "confirmed",
        # 30 minutes ago
        start_time: DateTime.add(DateTime.utc_now(), -1800, :second),
        # 30 minutes from now
        end_time: DateTime.add(DateTime.utc_now(), 1800, :second)
      }

      assert {:error, "Cannot cancel a meeting that has already started"} =
               Policy.can_cancel_meeting?(current_meeting)
    end

    test "blocks cancellation for past meetings" do
      past_meeting = %MeetingSchema{
        uid: "test-uid",
        status: "confirmed",
        # 2 hours ago
        start_time: DateTime.add(DateTime.utc_now(), -7200, :second),
        # 1 hour ago
        end_time: DateTime.add(DateTime.utc_now(), -3600, :second)
      }

      assert {:error, "Cannot cancel a meeting that has already occurred"} =
               Policy.can_cancel_meeting?(past_meeting)
    end

    test "blocks cancellation for already cancelled meetings" do
      cancelled_meeting = %MeetingSchema{
        uid: "test-uid",
        status: "cancelled",
        start_time: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_time: DateTime.add(DateTime.utc_now(), 7200, :second)
      }

      assert {:error, "Meeting is already cancelled"} =
               Policy.can_cancel_meeting?(cancelled_meeting)
    end

    test "blocks cancellation for completed meetings" do
      completed_meeting = %MeetingSchema{
        uid: "test-uid",
        status: "completed",
        start_time: DateTime.add(DateTime.utc_now(), -7200, :second),
        end_time: DateTime.add(DateTime.utc_now(), -3600, :second)
      }

      assert {:error, "Cannot cancel a completed meeting"} =
               Policy.can_cancel_meeting?(completed_meeting)
    end

    test "allows cancellation for meeting starting in 1 minute" do
      # Meeting starts in exactly 1 minute - should still be allowed
      almost_starting = %MeetingSchema{
        uid: "test-uid",
        status: "confirmed",
        # 1 minute from now
        start_time: DateTime.add(DateTime.utc_now(), 60, :second),
        # 61 minutes from now
        end_time: DateTime.add(DateTime.utc_now(), 3660, :second)
      }

      assert Policy.can_cancel_meeting?(almost_starting) == :ok
    end

    test "blocks cancellation for expired meetings" do
      # A lapsed approval request. It was already released and refunded, and
      # the withdraw link in the request-received email still points here —
      # cancelling would overwrite the expiry outcome and send a second,
      # contradicting round of emails.
      expired_request = %MeetingSchema{
        uid: "test-uid",
        status: "expired",
        start_time: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_time: DateTime.add(DateTime.utc_now(), 7200, :second)
      }

      assert {:error, "Cannot cancel an expired meeting"} =
               Policy.can_cancel_meeting?(expired_request)
    end
  end

  describe "can_reschedule_meeting?/1" do
    test "allows rescheduling for future meetings" do
      future_meeting = %MeetingSchema{
        uid: "test-uid",
        status: "confirmed",
        start_time: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_time: DateTime.add(DateTime.utc_now(), 7200, :second)
      }

      assert Policy.can_reschedule_meeting?(future_meeting) == :ok
    end

    test "blocks rescheduling for meetings that have started" do
      current_meeting = %MeetingSchema{
        uid: "test-uid",
        status: "confirmed",
        start_time: DateTime.add(DateTime.utc_now(), -1800, :second),
        end_time: DateTime.add(DateTime.utc_now(), 1800, :second)
      }

      assert {:error, "Cannot reschedule a meeting that has already started"} =
               Policy.can_reschedule_meeting?(current_meeting)
    end

    test "blocks rescheduling for past meetings" do
      past_meeting = %MeetingSchema{
        uid: "test-uid",
        status: "confirmed",
        start_time: DateTime.add(DateTime.utc_now(), -7200, :second),
        end_time: DateTime.add(DateTime.utc_now(), -3600, :second)
      }

      assert {:error, "Cannot reschedule a meeting that has already occurred"} =
               Policy.can_reschedule_meeting?(past_meeting)
    end

    test "blocks rescheduling for cancelled meetings" do
      cancelled_meeting = %MeetingSchema{
        uid: "test-uid",
        status: "cancelled",
        start_time: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_time: DateTime.add(DateTime.utc_now(), 7200, :second)
      }

      assert {:error, "Cannot reschedule a cancelled meeting"} =
               Policy.can_reschedule_meeting?(cancelled_meeting)
    end

    test "blocks rescheduling for completed meetings" do
      completed_meeting = %MeetingSchema{
        uid: "test-uid",
        status: "completed",
        start_time: DateTime.add(DateTime.utc_now(), -7200, :second),
        end_time: DateTime.add(DateTime.utc_now(), -3600, :second)
      }

      assert {:error, "Cannot reschedule a completed meeting"} =
               Policy.can_reschedule_meeting?(completed_meeting)
    end

    test "blocks rescheduling for expired meetings" do
      # An expired meeting no longer occupies its slot, so moving it would hand
      # the attendee a time nothing reserves.
      expired_meeting = %MeetingSchema{
        uid: "test-uid",
        status: "expired",
        start_time: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_time: DateTime.add(DateTime.utc_now(), 7200, :second)
      }

      assert {:error, "Cannot reschedule an expired meeting"} =
               Policy.can_reschedule_meeting?(expired_meeting)
    end

    test "allows rescheduling for meeting starting in 1 minute" do
      # Meeting starts in exactly 1 minute - should still be allowed
      almost_starting = %MeetingSchema{
        uid: "test-uid",
        status: "confirmed",
        start_time: DateTime.add(DateTime.utc_now(), 60, :second),
        end_time: DateTime.add(DateTime.utc_now(), 3660, :second)
      }

      assert Policy.can_reschedule_meeting?(almost_starting) == :ok
    end
  end

  describe "meeting_is_current?/1" do
    test "returns true for ongoing meeting" do
      current_meeting = %{
        start_time: DateTime.add(DateTime.utc_now(), -1800, :second),
        end_time: DateTime.add(DateTime.utc_now(), 1800, :second)
      }

      assert Policy.meeting_is_current?(current_meeting) == true
    end

    test "returns false for future meeting" do
      future_meeting = %{
        start_time: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_time: DateTime.add(DateTime.utc_now(), 7200, :second)
      }

      assert Policy.meeting_is_current?(future_meeting) == false
    end

    test "returns false for past meeting" do
      past_meeting = %{
        start_time: DateTime.add(DateTime.utc_now(), -7200, :second),
        end_time: DateTime.add(DateTime.utc_now(), -3600, :second)
      }

      assert Policy.meeting_is_current?(past_meeting) == false
    end

    test "returns true for meeting that just started" do
      just_started = %{
        start_time: DateTime.utc_now(),
        end_time: DateTime.add(DateTime.utc_now(), 3600, :second)
      }

      assert Policy.meeting_is_current?(just_started) == true
    end
  end

  describe "meeting_is_past?/1" do
    test "returns true for past meeting" do
      past_meeting = %{
        end_time: DateTime.add(DateTime.utc_now(), -3600, :second)
      }

      assert Policy.meeting_is_past?(past_meeting) == true
    end

    test "returns false for future meeting" do
      future_meeting = %{
        end_time: DateTime.add(DateTime.utc_now(), 3600, :second)
      }

      assert Policy.meeting_is_past?(future_meeting) == false
    end

    test "returns false for ongoing meeting" do
      current_meeting = %{
        end_time: DateTime.add(DateTime.utc_now(), 1800, :second)
      }

      assert Policy.meeting_is_past?(current_meeting) == false
    end

    test "returns false for meeting ending soon" do
      # Use a larger future offset (30 seconds) to avoid timing issues in tests
      ending_soon = %{
        end_time: DateTime.add(DateTime.utc_now(), 30, :second)
      }

      # Meeting ending in 30 seconds is not considered past
      assert Policy.meeting_is_past?(ending_soon) == false
    end
  end

  describe "build_meeting_attributes/1 description" do
    test "takes description from the meeting type, not the attendee's message" do
      user = insert(:user)
      _profile = insert(:profile, user: user)
      meeting_type = insert(:meeting_type, user: user, description: "Quarterly review")

      params = %{
        meeting_uid: "meeting-uid",
        start_datetime: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_datetime: DateTime.add(DateTime.utc_now(), 5400, :second),
        duration_minutes: 30,
        form_data: %{
          "name" => "Attendee",
          "email" => "attendee@example.com",
          "message" => "Please bring slides."
        },
        organizer_user_id: user.id,
        meeting_type_id: meeting_type.id,
        user_timezone: "UTC"
      }

      expect(Tymeslot.CalendarMock, :get_booking_integration_info, fn _client ->
        {:ok, %{integration_id: 1, calendar_path: "primary"}}
      end)

      attrs = Policy.build_meeting_attributes(BuildParams.new(params))

      assert attrs.description == "Quarterly review"
      assert attrs.attendee_message == "Please bring slides."
    end

    test "defaults description to empty string when no meeting type is supplied" do
      user = insert(:user)
      _profile = insert(:profile, user: user)

      params = %{
        meeting_uid: "meeting-uid",
        start_datetime: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_datetime: DateTime.add(DateTime.utc_now(), 5400, :second),
        duration_minutes: 30,
        form_data: %{
          "name" => "Attendee",
          "email" => "attendee@example.com",
          "message" => "Hi!"
        },
        organizer_user_id: user.id,
        meeting_type_id: nil,
        user_timezone: "UTC"
      }

      expect(Tymeslot.CalendarMock, :get_booking_integration_info, fn _client ->
        {:ok, %{integration_id: 1, calendar_path: "primary"}}
      end)

      attrs = Policy.build_meeting_attributes(BuildParams.new(params))

      assert attrs.description == ""
      assert attrs.attendee_message == "Hi!"
    end
  end

  describe "build_meeting_attributes/1 per-locale meeting-type translations" do
    test "snapshots the translated name/description when the attendee locale matches a row" do
      user = insert(:user)
      _profile = insert(:profile, user: user)

      base_type =
        insert(:meeting_type, user: user, name: "Quick Chat", description: "Quarterly review")

      {:ok, meeting_type} =
        MeetingTypes.update_meeting_type(base_type, %{
          "translations" => [
            %{"locale" => "de", "name" => "Kurzes Gespräch", "description" => "Quartalsbericht"}
          ]
        })

      params = %{
        meeting_uid: "meeting-uid",
        start_datetime: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_datetime: DateTime.add(DateTime.utc_now(), 5400, :second),
        duration_minutes: 30,
        form_data: %{"name" => "Attendee", "email" => "attendee@example.com", "message" => ""},
        organizer_user_id: user.id,
        meeting_type_id: meeting_type.id,
        user_timezone: "UTC",
        attendee_locale: "de"
      }

      expect(Tymeslot.CalendarMock, :get_booking_integration_info, fn _client ->
        {:ok, %{integration_id: 1, calendar_path: "primary"}}
      end)

      attrs = Policy.build_meeting_attributes(BuildParams.new(params))

      assert attrs.description == "Quartalsbericht"
      assert attrs.meeting_type == "Kurzes Gespräch"
      assert attrs.title =~ "Kurzes Gespräch"
      assert attrs.summary =~ "Kurzes Gespräch"
      assert attrs.attendee_locale == "de"
    end

    test "snapshots the base name/description when no translation matches the attendee locale" do
      user = insert(:user)
      _profile = insert(:profile, user: user)

      base_type =
        insert(:meeting_type, user: user, name: "Quick Chat", description: "Quarterly review")

      {:ok, meeting_type} =
        MeetingTypes.update_meeting_type(base_type, %{
          "translations" => [
            %{"locale" => "de", "name" => "Kurzes Gespräch", "description" => "Quartalsbericht"}
          ]
        })

      params = %{
        meeting_uid: "meeting-uid",
        start_datetime: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_datetime: DateTime.add(DateTime.utc_now(), 5400, :second),
        duration_minutes: 30,
        form_data: %{"name" => "Attendee", "email" => "attendee@example.com", "message" => ""},
        organizer_user_id: user.id,
        meeting_type_id: meeting_type.id,
        user_timezone: "UTC",
        attendee_locale: "fr"
      }

      expect(Tymeslot.CalendarMock, :get_booking_integration_info, fn _client ->
        {:ok, %{integration_id: 1, calendar_path: "primary"}}
      end)

      attrs = Policy.build_meeting_attributes(BuildParams.new(params))

      assert attrs.description == "Quarterly review"
      assert attrs.meeting_type == "Quick Chat"
    end

    test "still yields \"General Meeting\" when no meeting type is supplied, regardless of locale" do
      user = insert(:user)
      _profile = insert(:profile, user: user)

      params = %{
        meeting_uid: "meeting-uid",
        start_datetime: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_datetime: DateTime.add(DateTime.utc_now(), 5400, :second),
        duration_minutes: 30,
        form_data: %{"name" => "Attendee", "email" => "attendee@example.com", "message" => ""},
        organizer_user_id: user.id,
        meeting_type_id: nil,
        user_timezone: "UTC",
        attendee_locale: "de"
      }

      expect(Tymeslot.CalendarMock, :get_booking_integration_info, fn _client ->
        {:ok, %{integration_id: 1, calendar_path: "primary"}}
      end)

      attrs = Policy.build_meeting_attributes(BuildParams.new(params))

      assert attrs.meeting_type == "General Meeting"
      assert attrs.description == ""
    end
  end

  describe "build_meeting_attributes/1 title" do
    setup do
      stub(Tymeslot.CalendarMock, :get_booking_integration_info, fn _client ->
        {:ok, %{integration_id: 1, calendar_path: "primary"}}
      end)

      :ok
    end

    defp build_title_attrs(organizer_locale) do
      user = insert(:user, locale: organizer_locale)
      _profile = insert(:profile, user: user)
      meeting_type = insert(:meeting_type, user: user, name: "IRIS Demo")

      params = %{
        meeting_uid: "meeting-uid",
        start_datetime: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_datetime: DateTime.add(DateTime.utc_now(), 5400, :second),
        duration_minutes: 30,
        form_data: %{"name" => "Jane Doe", "email" => "jane@example.com"},
        organizer_user_id: user.id,
        meeting_type_id: meeting_type.id,
        user_timezone: "UTC",
        attendee_locale: "en"
      }

      Policy.build_meeting_attributes(BuildParams.new(params))
    end

    test "renders the title in the organiser's language, not the attendee's" do
      attrs = build_title_attrs("de")

      assert attrs.title == "IRIS Demo mit Jane Doe"
      assert attrs.summary == "IRIS Demo mit Jane Doe"
    end

    test "keeps the English title for an English-speaking organiser" do
      assert build_title_attrs("en").title == "IRIS Demo with Jane Doe"
    end
  end

  describe "build_meeting_attributes/1 reminders snapshot" do
    test "uses meeting type reminder config when present" do
      user = insert(:user)
      _profile = insert(:profile, user: user)

      meeting_type =
        insert(:meeting_type,
          user: user,
          reminder_config: [%{value: 10, unit: "minutes"}, %{value: 1, unit: "hours"}]
        )

      params = %{
        meeting_uid: "meeting-uid",
        start_datetime: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_datetime: DateTime.add(DateTime.utc_now(), 5400, :second),
        duration_minutes: 30,
        form_data: %{"name" => "Attendee", "email" => "attendee@example.com", "message" => ""},
        organizer_user_id: user.id,
        meeting_type_id: meeting_type.id,
        user_timezone: "UTC"
      }

      expect(Tymeslot.CalendarMock, :get_booking_integration_info, fn _client ->
        {:ok, %{integration_id: 1, calendar_path: "primary"}}
      end)

      attrs = Policy.build_meeting_attributes(BuildParams.new(params))

      assert attrs.reminders == [%{value: 10, unit: "minutes"}, %{value: 1, unit: "hours"}]
    end

    test "respects explicit empty reminder config" do
      user = insert(:user)
      _profile = insert(:profile, user: user)

      meeting_type =
        insert(:meeting_type,
          user: user,
          reminder_config: []
        )

      params = %{
        meeting_uid: "meeting-uid",
        start_datetime: DateTime.add(DateTime.utc_now(), 3600, :second),
        end_datetime: DateTime.add(DateTime.utc_now(), 5400, :second),
        duration_minutes: 30,
        form_data: %{"name" => "Attendee", "email" => "attendee@example.com", "message" => ""},
        organizer_user_id: user.id,
        meeting_type_id: meeting_type.id,
        user_timezone: "UTC"
      }

      expect(Tymeslot.CalendarMock, :get_booking_integration_info, fn _client ->
        {:ok, %{integration_id: 1, calendar_path: "primary"}}
      end)

      attrs = Policy.build_meeting_attributes(BuildParams.new(params))

      assert attrs.reminders == []
    end
  end
end
