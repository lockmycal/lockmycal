defmodule Tymeslot.Profiles.ProfileSchemaUsernameTest do
  use Tymeslot.DataCase, async: true

  @moduletag :database
  @moduletag :schema

  alias Tymeslot.Profiles.ProfileSchema

  describe "username validation" do
    test "accepts valid usernames" do
      valid_usernames = [
        "john123",
        "mary-jane",
        "user2025",
        "a12",
        "123abc",
        "my-awesome-username",
        "user_name"
      ]

      for username <- valid_usernames do
        user = insert(:user)

        changeset =
          ProfileSchema.changeset(%ProfileSchema{}, %{
            user_id: user.id,
            username: username,
            timezone: "Europe/Kyiv"
          })

        assert changeset.valid?, "Username '#{username}' should be valid"
      end
    end

    test "rejects invalid usernames" do
      invalid_usernames = [
        {"ab", "Username must be at least 3 characters long"},
        {"a" <> String.duplicate("b", 30), "Username must be at most 30 characters long"},
        {"John123",
         "Username must start with a letter or number and contain only lowercase letters, numbers, underscores, and hyphens"},
        {"user@name",
         "Username must start with a letter or number and contain only lowercase letters, numbers, underscores, and hyphens"},
        {"-username",
         "Username must start with a letter or number and contain only lowercase letters, numbers, underscores, and hyphens"},
        {"user name",
         "Username must start with a letter or number and contain only lowercase letters, numbers, underscores, and hyphens"},
        {"user.name",
         "Username must start with a letter or number and contain only lowercase letters, numbers, underscores, and hyphens"}
      ]

      for {username, expected_error} <- invalid_usernames do
        changeset =
          ProfileSchema.changeset(%ProfileSchema{}, %{username: username, timezone: "Europe/Kyiv"})

        refute changeset.valid?, "Username '#{username}' should be invalid"
        assert expected_error in (errors_on(changeset)[:username] || [])
      end
    end

    test "rejects reserved usernames" do
      reserved = [
        "admin",
        "api",
        "app",
        "auth",
        "blog",
        "dashboard",
        "dev",
        "docs",
        "help",
        "home",
        "login",
        "logout",
        "meeting",
        "meetings",
        "profile",
        "register",
        "schedule",
        "settings",
        "signup",
        "static",
        "support",
        "test",
        "user",
        "users",
        "www",
        "healthcheck",
        "assets",
        "images",
        "css",
        "fonts",
        "about",
        "contact",
        "privacy",
        "terms"
      ]

      for username <- reserved do
        changeset =
          ProfileSchema.changeset(%ProfileSchema{}, %{username: username, timezone: "Europe/Kyiv"})

        refute changeset.valid?, "Username '#{username}' should be reserved"
        errors = errors_on(changeset)[:username] || []

        assert "is reserved" in errors,
               "Expected 'is reserved' in #{inspect(errors)} for username '#{username}'"
      end
    end

    test "rejects reserved phrases written with hyphens" do
      # Usernames are lowercase by format, so a reserved entry is only ever
      # matched in its lowercase spelling.
      for username <- ["jesus-christ", "christ-sake"] do
        changeset =
          ProfileSchema.changeset(%ProfileSchema{}, %{username: username, timezone: "Europe/Kyiv"})

        assert "is reserved" in (errors_on(changeset)[:username] || []),
               "Username '#{username}' should be reserved"
      end
    end

    test "username is optional" do
      user = insert(:user)

      changeset =
        ProfileSchema.changeset(%ProfileSchema{}, %{user_id: user.id, timezone: "Europe/Kyiv"})

      assert changeset.valid?
    end
  end
end
